= V4 Outline MultiLine NoSorting TabWidth=30

H="Rand Family Data Processing"
Rand Family Data Processing



H="reshape long"
/*Reshape long format.  Keep only those in wave. Gen core_year variable
creates two datasets:
	respondent level: "E:\data\hrs_public_2012\rand2012\family\working\family_r_all.dta" 
	kid-respondent level: "E:\data\hrs_public_2012\rand2012\family\working\family_k_all.dta" 
*/

clear all
set more off 

local work E:\data\hrs_public_2012\rand2012\family\working 
use "E:\data\hrs_public_2012\rand2012\family\StataSE\rndfamr_c.dta", clear

forvalues i=1/9 {
	foreach x in r s h {
		rename `x'`i'* `x'_*`i'
}
}
foreach x in r s h {
	rename `x'_0* `x'_*0
}

forvalues i=1/10 {
	preserve
	
	keep hhidpn rlink *`i'
	rename *`i' *
	gen wave=`i'
	save "`work'\family_r_w_`i'", replace
	restore
}

use `work'\family_r_w_1, clear

forvalues i=2/10 {
	append using `work'\family_r_w_`i'
}

keep if inw==1

gen core_year=1992 if wave==1
replace core_year=1994 if wave==2
replace core_year=1996 if wave==3
replace core_year=1998 if wave==4
replace core_year=2000 if wave==5
replace core_year=2002 if wave==6
replace core_year=2004 if wave==7
replace core_year=2006 if wave==8
replace core_year=2008 if wave==9
replace core_year=2010 if wave==10
replace core_year=2012 if wave==11


save `work'\family_r_all, replace

use "E:\data\hrs_public_2012\rand2012\family\StataSE\rndfamk_c.dta", clear

forvalues i=1/9 {
	foreach x in k s kp h {
		rename `x'`i'* `x'_*`i'
}
}

foreach x in k s kp h {
	rename `x'_0* `x'_*0
}

forvalues i=1/10 {
	preserve
	
	keep hhidpn link *`i'
	rename *`i' *
	gen wave=`i'
	save "`work'\family_k_w_`i'", replace
	restore
}

use `work'\family_k_w_1, clear

forvalues i=2/10 {
	append using `work'\family_k_w_`i'
}

keep if inw==1

gen core_year=1992 if wave==1
replace core_year=1994 if wave==2
replace core_year=1996 if wave==3
replace core_year=1998 if wave==4
replace core_year=2000 if wave==5
replace core_year=2002 if wave==6
replace core_year=2004 if wave==7
replace core_year=2006 if wave==8
replace core_year=2008 if wave==9
replace core_year=2010 if wave==10
replace core_year=2012 if wave==11



save `work'\family_k_all, replace




H="select, clean, create variables"
/*drop observations that are out of scope or have bad linkage; clean and create new variables.
note-as of now, only done for respondent-level database
final database is "E:\data\hrs_public_2012\rand2012\family\family_r_clean_98_10.dta"*/


set more off 

local work E:\data\hrs_public_2012\rand2012\family\working 
cd `work'

use family_r_all, clear

//drop observations that are out of scope or have a cross-wave linkage problem
drop if rlink==0 | core_year<1998
gen inscope=1

//keep select variables
local keepvars h_child h_lvnear h_resdk r_hlpadlkn r_hlpiadlkn ///
r_hlpfinkn r_hlpchrkn r_hlpfutkn r_hlpfutgn r_hltcstkn h_kdcarekn h_tcany ///
h_tcntran h_tcamt h_fcany h_fcntran h_fcamt r_kdcarehr
keep hhidpn core_year inscope `keepvars'

foreach x of local keepvars {
	replace `x'=. if inlist(`x',.c,.d,.d,.e,.f,.h,.k,.m,.r,.s)
	tab `x',m
}

//number of kids; any kids indicator
gen num_living_kids=h_child
gen living_kids_ind=h_child>0 if h_child!=.

label var num_living_kids "Number living, in-contact children"
label var living_kids_ind "Any living, in-contact children"

//number co-resident children; live with kids indicator
//also have h_livnear, which has ~10% disagreement on co-residents
gen num_resd_kids=h_resdkn
gen resd_kid_ind=h_resdkn>0 if h_resdkn!=.

label var num_resd_kids "Number co-resident children"
label var resd_kid_ind "Lives with at least one child"

//transfers, amount and ind, to and from children
gen transf_to_kid_ind=h_tcany==1 if h_tcany!=.
gen amt_transf_to_kid=h_tcamt
gen transf_from_kid_ind=h_fcany==1 if h_fcany!=.
gen amt_transf_from_kid=h_fcamt

label var transf_to_kid_ind "Any transfers to children, $500+"
label var amt_transf_to_kid "Amount transfers to children, any amt"
label var transf_from_kid_ind "Any transfers from children, $500+"
label var amt_transf_from_kid "Amount transfers from children, any amt"

//help variables, adl, iadl, finances, chores & errands, healthcare, ///
//future kids & grandkids
gen help_adls_kids_ind=r_hlpadlkn>0 if r_hlpadlkn!=.
gen help_iadls_kids_ind=r_hlpiadlkn>0 if r_hlpiadlkn!=.
gen help_fin_kids_ind=r_hlpfinkn>0 if r_hlpfinkn!=.
gen help_chores_kids_ind=r_hlpchrkn>0 if r_hlpchrkn!=.
gen help_hcosts_kids_ind=r_hltcstkn>0 if r_hltcstkn!=.
gen help_future_kids_ind=r_hlpfutkn>0 if r_hlpfutkn!=.
gen help_future_gkids_ind=r_hlpfutgn>0 if r_hlpfutgn!=.

label var help_adls "Children help with ADLs"
label var help_iadls "Children help with IADLs"
label var help_fin "Children help with Finances"
label var help_chores "Children help with chores or errands"
label var help_hcosts "Children help w/ HC costs"
label var help_future_k "Children will help in future"
label var help_future_gk "Grandkids will help in future"

//kids receive 100+ hrs help from r and/or s with child care & r child care amt
gen child_care_ind=r_kdcarehr>0 if r_kdcarehr!=.
gen amt_child_care=r_kdcarehr
label var child_care_ind "Children use R for child care"
label var amt_child_care "Number hrs child care provided by R"
drop `keepvars'

local newvars num_living_kids living_kids_ind num_resd_kids resd_kid_ind ///
transf_to_kid_ind amt_transf_to_kid transf_from_kid_ind amt_transf_from_kid ///
help_adls_kids_ind help_iadls_kids_ind help_fin_kids_ind help_chores_kids_ind ///
help_hcosts_kids_ind help_future_kids_ind help_future_gkids_ind child_care_ind ///
amt_child_care

reshape wide inscope `newvars', i(hhidpn) j(core_year)


forvalues i= 2000(2)2010 {
	gen moved_in`i'=resd_kid_ind`=`i'-2'==0 & resd_kid_ind`i'==1 
	replace moved_in`i'=. if resd_kid_ind`=`i'-2'==. | resd_kid_ind`i'==. ///
	| resd_kid_ind`=`i'-2'==1
	gen moved_out`i'=resd_kid_ind`=`i'-2'==1 & resd_kid_ind`i'==0
	replace moved_out`i'=. if resd_kid_ind`=`i'-2'==. | resd_kid_ind`i'==. ///
	| resd_kid_ind`=`i'-2'==0
}

reshape long inscope `newvars' moved_out moved_in, i(hhidpn) j(core_year)
keep if inscope==1

label var moved_in "Now lives with child; moved in since last ivw"
label var moved_out "No longer lives with child; moved out"

save "E:\data\hrs_public_2012\rand2012\family\family_r_clean_98_10.dta", replace


H="descriptive statistics"
//look at crosstabs, etc.


set more off 

cd "E:\data\hrs_public_2012\rand2012\family"

use family_r_clean_98_10, clear

local cfamvars num_living_kids num_resd_kids amt_transf_to_kid amt_transf_from

local famvars num_living_kids living_kids_ind num_resd_kids resd_kid_ind ///
moved_in moved_out transf_to_kid_ind amt_transf_to_kid transf_from_kid_ind ///
amt_transf_from_kid help_adls_kids_ind help_iadls_kids_ind help_fin_kids_ind ///
help_hcosts_kids_ind help_future_kids_ind help_future_gkids_ind child_care_ind

local rn : word count `famvars' n

mat fam=J(`rn',4,.)
local r=1
local c=1

foreach x of local famvars {
	sum `x' 
	if inlist("`x'","num_living_kids", "num_resd_kids", "amt_transf_to_kid", ///
	"amt_transf_from_kid") {
		mat fam[`r',1]=r(mean)
		mat fam[`r',2]=r(sd)
}
	else {
		mat fam[`r',1]=r(mean)*100
		mat fam[`r',2]=r(mean)*r(N)
}	
	sum core_year if `x'==.
	mat fam[`r',3]=r(N)
	mat fam[`r',4]=(r(N)/_N)*100
	local r=`r'+1
}

sum core_year
mat fam[`r',2]=r(N)
mat fam[`r',3]=_N-r(N)


mat rownames fam=`famvars' Total

frmttable, statmat(fam) varlabels ctitles("" "Mean/%" "sd/N" "N missing" ///
"% missing") title("Respondent-level kid variables, Rand") ///
note("Data from Rand respondent-level family file with good cross-wave linkage, 1998-2010") ///
sdec(2,0,0,2)

