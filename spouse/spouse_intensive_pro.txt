= V4 Outline MultiLine NoSorting TabWidth=30

H="R intensive procedures, effect on surving spouse depression"
Starts with dataset created for the spousal depression outcomes project
created in the file spouse_2010.txt
This ds is created in the "Create additional variables" tab of spouse_2010.txt

H="Define sample"
/*This switches to Stata using data dataset exported in spouse_2010.txt code*/

/*Full decedent sample (2002-2010 exit interviews) is restricted to:
1. Decedents with core interview at least 6m before death
2. With medicare xwalk id
3. With FFS medicare last 6 months of life
4. Married at time of death
5. With spouse p1 interview
6. With spouse core interview at least 6m before R's death
7. Spouse has full CES-D information both pre and post R's death

Additional variables to consider for sensitivity analysis:
1. R died suddenly as defined by either no comorbidities 12 m before
death or cause of death = accident, suicide, homicide
(no comorbidities = data warehouse cc's, excluding cataract and glaucoma)
2. P1 interview within 90 days of R's death

Also creates table looking at rates of intensive procedures in the sample
*/

capture log close

clear all
set mem 500m
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local origdata E:\data\spouse\final_data
local datapath E:\data\spouse_intensive\data

log using "`logpath'\1-HRS_Spouse_intensive_sample.txt", text replace

cd `datapath'

use `origdata'\spouse_data_full.dta
************************************************************
//create variables needed to define sample

//get indicator for any of 5 intensive procedures, excluding hemodialysis
gen int_any_of5_6m = .
replace int_any_of5_6m = 1 if int_intubation_6m==1 | int_trach_6m==1 | ///
 int_gastro_tude_6m==1 | int_enteral_nut_6m==1 | int_cpr_6m==1
replace int_any_of5_6m = 0 if int_intubation_6m==0 & int_trach_6m==0 & ///
 int_gastro_tude_6m==0 & int_enteral_nut_6m==0 & int_cpr_6m==0
la var int_any_of5_6m "Any intensive pro last 6m, excl hemodial"
tab int_any_of5_6m, missing
tab int_any_of5_6m int_any_6m, missing
tab int_any_of5_6m int_hemodia_6m, missing

//check date of death for full decedent sample
tab r_exit_year_x if r_death_date_e==., missing
tab r_xwalk_yes if r_death_date_e==., missing
//if have xwalk, check if there is a dod present in the claims
gen r_ind_claims_dod = 0
replace r_ind_claims_dod=1 if r_claims_dod~=.
tab r_ind_claims_dod, missing
tab r_ind_claims_dod if r_death_date_e==., missing

//use the claims dod if the HRS dod is missing
gen r_death_date = r_death_date_e
replace r_death_date = r_claims_dod if  r_death_date_e==.
la var r_death_date "R DOD from HRS or claims"
gen r_use_claims_dod = 0
replace r_use_claims_dod = 1 if r_death_date_e==. & r_ind_claims_dod==1
tab r_use_claims_dod, missing

gen r_ind_dod = 0
replace r_ind_dod = 1 if r_death_date ~=.
la var r_ind_dod "Indicator R death date present"
tab r_ind_dod, missing

//Decedent has n1 core interview or n2 if n1 w/i 6m of death
gen r_days_n1_core_dead = r_death_date - r_c_ivw_date_n1
tab r_ind_dod if r_days_n1_core_dead==., missing
//missing days all have no core n1 interview
tab r_core_year_n1 if r_days_n1_core_dead==., missing

gen r_ind_n1_wi_6m_dead = .
replace r_ind_n1_wi_6m_dead=1 if r_days_n1_core_dead <183 & r_days_n1_core_dead!=.
replace r_ind_n1_wi_6m_dead=0 if r_days_n1_core_dead >=183 & r_days_n1_core_dead!=.
label var r_ind_n1_wi_6m_dead "r n1 core within 6m of death"
tab r_ind_n1_wi_6m_dead , missing

gen r_core_pre_ind=0
replace r_core_pre_ind=1 if r_core_year_n1!=. & r_ind_n1_wi_6m_dead==0
replace r_core_pre_ind=1 if r_core_year_n2!=. & r_ind_n1_wi_6m_dead==1
la var r_core_pre_ind "R has core interview gt 6m before death"
tab r_core_pre_ind, missing
tab r_core_pre_ind r_exit_year_x , missing

//Decedent had parts a and b coverage and no hmo 6m prior to death
gen byte ins_ind=.
replace ins_ind=1 if r_part_ab_6m==1 & r_hmo_d_6m==0
replace ins_ind=0 if r_part_ab_6m!=1 | r_hmo_d_6m!=0
la var ins_ind "FFS Medicare 6m pre-death"
tab ins_ind, missing

//has spouse p1 interview
gen byte sp1core_ind=.
replace sp1core_ind=0 if s_core_year_p1==.
replace sp1core_ind=1 if s_core_year_p1!=.
tab sp1core_ind, missing
tab sp1core_ind r_exit_year_x , missing
tab s_core_year_p1 r_exit_year_x , missing

//has spouse n1 interview or n2 if n1 w/i 6m of R's death
gen s_days_n1_core_dead = r_death_date - s_c_ivw_date_n1
tab s_core_year_n1 if s_days_n1_core_dead==., missing
 
gen s_ind_n1_wi_6m_dead = .
replace s_ind_n1_wi_6m_dead=1 if s_days_n1_core_dead <183 & s_days_n1_core_dead!=.
replace s_ind_n1_wi_6m_dead=0 if s_days_n1_core_dead >=183 & s_days_n1_core_dead!=.
label var s_ind_n1_wi_6m_dead "S n1 core within 6m of death"
tab s_ind_n1_wi_6m_dead, missing

gen s_core_pre_ind=0
replace s_core_pre_ind=1 if s_core_year_n1!=. & s_ind_n1_wi_6m_dead==0
replace s_core_pre_ind=1 if s_core_year_n2!=. & s_ind_n1_wi_6m_dead==1
la var s_core_pre_ind "S has core interview gt 6m before death"
tab s_core_pre_ind, missing
tab s_core_pre_ind r_exit_year_x , missing

//for spouse cesd = use n2 ivw if n1 is within 6m of R's death
tab s_cesd_tot_n1 s_ind_n1_wi_6m_dead, missing
gen s_cesd_tot_n1n2 = s_cesd_tot_n1 if s_ind_n1_wi_6m_dead==0
replace s_cesd_tot_n1n2 = s_cesd_tot_n2 if s_ind_n1_wi_6m_dead==1
tab s_cesd_tot_n1n2 s_core_pre_ind, missing

gen byte cesd_miss_n1n2 = 0
replace cesd_miss_n1n2 =1 if s_cesd_tot_n1n2==.
gen byte cesd_miss_p1 = 0
replace cesd_miss_p1 =1 if s_cesd_tot_p1==.
tab cesd_miss_n1n2 cesd_miss_p1 , missing
gen byte cesd_miss_n1_p1 = 0
replace cesd_miss_n1_p1 =1 if cesd_miss_n1n2==1 | cesd_miss_p1 ==1
la var cesd_miss_n1_p1 "CESD missing either n1 or p1"
tab cesd_miss_n1_p1 , missing

//missing cesd b/c proxy interview - so not asked
gen s_proxy_n1n2 = s_proxy_core_n1 if s_ind_n1_wi_6m_dead==0
replace s_proxy_n1n2 = s_proxy_core_n2 if s_ind_n1_wi_6m_dead==1
tab s_proxy_n1n2 , missing
gen byte proxy_n1_or_p1 = 0
replace proxy_n1_or_p1 =1 if s_proxy_core_p1==1 | s_proxy_n1n2==1
la var proxy_n1_or_p1 "Ind. that s p1 or n1 ivw is via proxy"
tab proxy_n1_or_p1, missing

tab proxy_n1_or_p1 cesd_miss_n1_p1 if r_core_pre_ind==1 & r_xwalk_yes==1 & ///
	ins_ind==1 & r_married_x==1 & sp1core_ind==1 & s_core_pre_ind==1 , missing

//cc's excluding cataract and glaucoma
gen cc_all_count_12m_2 = cc_1_ami_n12mn0 + cc_2_alzh_n12mn0 + cc_3_alzhdmta_n12mn0 + ///
cc_4_atrialfb_n12mn0 + /*cc_5_cataract_n12mn0 +*/ cc_6_chrnkidn_n12mn0 + ///
cc_7_copd_n12mn0 + cc_8_chf_n12mn0 + cc_9_diabetes_n12mn0 + /*cc_10_glaucoma_n12mn0 +*/ ///
cc_11_hipfrac_n12mn0 + cc_12_ischmcht_n12mn0 + cc_13_depressn_n12mn0 + ///
cc_14_osteoprs_n12mn0 + cc_15_ra_oa_n12mn0 + cc_16_strketia_n12mn0 + ///
cc_17_cncrbrst_n12mn0 + cc_18_cncrclrc_n12mn0 + cc_19_cncrprst_n12mn0 + ///
cc_20_cncrlung_n12mn0 + cc_21_cncrendm_n12mn0
la var cc_all_count_12m_2 "Count of cc's, excludes cataract and glaucoma"

tab cc_all_count_12m_2, missing

gen byte no_cc_12m_excl_cataract = .
replace no_cc_12m_excl_cataract =1 if cc_all_count_12m_2==0
replace no_cc_12m_excl_cataract =0 if (cc_all_count_12m_2>0 & cc_all_count_12m_2!=.)
la var no_cc_12m_excl_cataract "Ind. for no Chronic conditions 12m before death"
tab no_cc_12m_excl_cataract , missing

//cause of death = Accidents, Suicide, Homicide (var = 12)
tab r_cause_death_12_n_e, missing
replace r_cause_death_12_n_e=. if r_cause_death_12_n_e==14
gen byte sudden_cause_death = .
replace sudden_cause_death =1 if r_cause_death_12_n_e==12
replace sudden_cause_death =0 if (r_cause_death_12_n_e!=12 & r_cause_death_12_n_e!=.)
la var sudden_cause_death "Cause of death = accidents, suicide, homicide"
tab sudden_cause_death, missing

************************************************************
//set matrix for sample size determination table
mat sample=J(12,1,.)

//all decedents from 2002-2010 exit interviews
sum r_exit_year_x, detail
mat sample[1,1]=r(N)

//decedents with pre-death core interview
sum r_exit_year_x if r_core_pre_ind==1, detail
mat sample[2,1]=r(N)

//decedents with n1 core + xwalk
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1, detail
mat sample[3,1]=r(N)

//decedents with n1 core + xwalk + ffs mc last 6 months of life
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1, detail
mat sample[4,1]=r(N)

//check of married decedents
tab r_married_x, missing

//decedents with n1 core + xwalk + married
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1, detail
mat sample[5,1]=r(N)

//decedents with n1 core + xwalk + married + spouse p1 interview
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & sp1core_ind==1, detail
mat sample[6,1]=r(N)

//decedents with n1 core + xwalk + married + spouse p1 and n1 interview
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & sp1core_ind==1 & s_core_pre_ind==1, detail
mat sample[7,1]=r(N)

//full n1 p1 cesd interview?
//missing cesd b/c of proxy interview
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & /// 
sp1core_ind==1 & s_core_pre_ind==1 & proxy_n1_or_p1==0, detail
mat sample[8,1]=r(N)

//missing cesd for unknown reason (has interviews and not proxy interviews)
//decedents with n1 core + xwalk + married + spouse p1 interview 
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & /// 
sp1core_ind==1 & s_core_pre_ind==1 & proxy_n1_or_p1==0 & cesd_miss_n1_p1==0, detail
mat sample[9,1]=r(N)

//using chronic conditions 1 year prior to death, excluding cataract and glaucoma
//decedents with n1 core + xwalk + married + spouse p1 interview
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & /// 
sp1core_ind==1 & s_core_pre_ind==1 & proxy_n1_or_p1==0 & cesd_miss_n1_p1==0 & ///
no_cc_12m_excl_cataract==0, detail
mat sample[10,1]=r(N)

//cause of death = Accidents, Suicide, Homicide (var = 12)
//using ccs v2
//decedents with n1 core + xwalk + married + spouse p1 interview + p1 > 90 days post death
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & /// 
sp1core_ind==1 & s_core_pre_ind==1 & proxy_n1_or_p1==0 & cesd_miss_n1_p1==0 & ///
no_cc_12m_excl_cataract==0 & sudden_cause_death==0, detail
mat sample[11,1]=r(N)
************************************************************

//p1 interview within 30, 90 days of r's death
gen days_p1_core_dead =  s_c_ivw_date_p1 -  r_death_date_e
gen p1_wi_90d = .
replace p1_wi_90d=1 if days_p1_core_dead<=90
replace p1_wi_90d=0 if (days_p1_core_dead>90 & days_p1_core_dead!=.)
la var p1_wi_90d "p1 interview w/i 90 days r death"
tab p1_wi_90d, missing

gen p1_wi_30d = .
replace p1_wi_30d=1 if days_p1_core_dead<=30
replace p1_wi_30d=0 if (days_p1_core_dead>30 & days_p1_core_dead!=.)
la var p1_wi_30d "p1 interview w/i 30 days r death"
tab p1_wi_30d, missing

//decedents with n1 core + xwalk + married + spouse p1 interview + p1 > 90 days post death
sum r_exit_year_x if r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & /// 
sp1core_ind==1 & s_core_pre_ind==1 & no_cc_12m_excl_cataract==0 ///
& sudden_cause_death==0 & cesd_miss_n1_p1==0 & p1_wi_90d==0, detail
mat sample[12,1]=r(N)

mat list sample

frmttable using `logpath'\1-spouse_intens_pro_sample , statmat(sample) ///
	title("2010 sample size estimate - requiring an n1 core interview") ///
	ctitle("","n") ///
	rtitle("All decedents from 2002-2010 exit interviews" \ "With pre-death core interview" \ ///
	"With mc xwalk id" \ "With FFS mc last 6 months of life" \ "Married at time of death" \ ///
	"Spouse p1 core interview" \"Spouse pre-death core interview" \ ///
	"Not Proxy n1 or p1 s interview" \ "Full CESD interview n1 and p1" \ ///
	"No comorb 12m before death" \ "Accidents, Suicide, Homicide = cause of death" \ ///
	"p1 ivw 90+ days from r's death") ///
	note("Pre-death core interview defined as having core interview at least" \ ///
	"6m prior to R's death for both R and S. No comorb 12m before death uses data" \ ///
	"warehouse chronic conditions excluding cataract and glaucoma.") ///
	sdec(0) replace
**************************************************
//details re n1 vs n2 interviews and missingness
mat n1n2=J(12,2,.)

sum r_exit_year_x, detail //all decedents from 2002-2010 exit interviews
mat n1n2[1,1]=r(N)

//number requiring n1
//the n=305 missing are all missing a pre-death core interview
tab r_core_year_n1 if r_days_n1_core_dead==., missing
tab r_ind_n1_wi_6m_dead, missing matcell(n1)
mat n1n2[2,1]=n1[1,1]+n1[3,1] //require n1, do those missing and those with ivw before 6m
mat n1n2[2,2]=n1[2,1]

sum r_core_pre_ind if r_ind_n1_wi_6m_dead==0 | r_days_n1_core_dead==.  //have n1
mat n1n2[3,1]=r(mean)*r(N)

sum r_core_pre_ind if r_ind_n1_wi_6m_dead==1  //have n2
mat n1n2[3,2]=r(mean)*r(N)

sum r_exit_year_x if r_core_pre_ind==1, detail //overall with pre-death core interview
mat n1n2[4,1]=r(N)

//with next set of requirements (xwalk, married, s p1 core)
local req1 r_core_pre_ind==1 & r_xwalk_yes==1 & ins_ind==1 &  r_married_x==1 & sp1core_ind==1
sum r_exit_year_x if `req1', detail
mat n1n2[5,1]=r(N)

tab s_core_year_n1 if s_days_n1_core_dead==., missing
tab s_ind_n1_wi_6m_dead if `req1', missing matcell(n1)
mat n1n2[6,1]=n1[1,1]+n1[3,1] //require n1, do those missing and those with ivw before 6m
mat n1n2[6,2]=n1[2,1]

sum s_core_pre_ind if (s_ind_n1_wi_6m_dead==0 | s_ind_n1_wi_6m_dead==.) & `req1' //have n1
mat n1n2[7,1]=r(mean)*r(N)

sum s_core_pre_ind if s_ind_n1_wi_6m_dead==1 & `req1' //have n2
mat n1n2[7,2]=r(mean)*r(N)

sum r_exit_year_x if s_core_pre_ind==1 & `req1', detail //overall with pre-death core interview
mat n1n2[8,1]=r(N)

//not proxy n1 or p1, split by the n1 vs n2 interview groups
sum r_exit_year_x if `req1' & s_core_pre_ind==1 & proxy_n1_or_p1==0 & s_ind_n1_wi_6m_dead==0 , detail
mat n1n2[9,1]=r(N)

sum r_exit_year_x if `req1' & s_core_pre_ind==1 & proxy_n1_or_p1==0 & s_ind_n1_wi_6m_dead==1 , detail
mat n1n2[9,2]=r(N)

sum r_exit_year_x if `req1' & s_core_pre_ind==1 & proxy_n1_or_p1==0 , detail
mat n1n2[10,1]=r(N)

//missing ces-d, split by the n1 vs n2 interview
local req2 `req1' & s_core_pre_ind==1 & proxy_n1_or_p1==0
sum r_exit_year_x if `req2' & cesd_miss_n1_p1==0 & s_ind_n1_wi_6m_dead==0, detail
mat n1n2[11,1]=r(N)
sum r_exit_year_x if `req2' & cesd_miss_n1_p1==0 & s_ind_n1_wi_6m_dead==1, detail
mat n1n2[11,2]=r(N)
sum r_exit_year_x if `req2' & cesd_miss_n1_p1==0 , detail
mat n1n2[12,1]=r(N)

mat list n1n2
	
frmttable using `logpath'\1-spouse_intens_pro_sample , statmat(n1n2) ///
	title("Sample details re n1 vs n2 interview required if n1 w/i 6m of R's death") ///
	ctitle("","n","n" \ "" "Use n1" "Use n2") ///
	rtitle("All decedents from 2002-2010 exit interviews" \ "Where R n1 or n2 ivw is needed" \ ///
	"R has n1 or n2 ivw" \ "R has pre-death core ivw" \ "W/ FFS MC, married, S has p1 ivw" \ ///
	"S n1 or n2 ivw needed" \ "S has n1 or n2 ivw" \"S has pre-death core interview" \ ///
	"S Not proxy by n1 or n2" \ "S not proxy pre or post ivw" \ "S Full CESD by n1 or n2" \ ///
	"Full CESD interview n1 and p1") ///
	note("Pre-death core interview defined as having core interview at least" \ ///
	"6m prior to R's death for both R and S.") ///
	sdec(0) addtable
	
**************************************************
/*drop observations not in the sample*/
drop if r_core_pre_ind==0
drop if r_xwalk_yes==0
drop if ins_ind==0 
drop if r_married_x==0 
drop if sp1core_ind==0
drop if s_core_pre_ind==0

tab r_exit_year_x r_core_year_n1

la def r_cause_death_12_n_e 1 "Infectious disease" 2 "HIV/AIDS" ///
3 "Cardiovascular disease" 4 "Chronic lower respiratory disease" ///
5 "Other Respiratory disease" 6 "Diabetes" 7 "Alzheimers Disease" ///
9 "Neoplasms" 10 "Kidney Disease (not infectious)" ///
11 "Liver, Gallbladder, Stomach and/or Intestinal disease" ///
12 "Accidents, Suicide, Homicide" 13 "Other"

la val r_cause_death_12_n_e r_cause_death_12_n_e
tab r_cause_death_12_n_e, missing

**************************************************
//tables looking at intensive procedure use in the sample
//just do for those with spouse with full cesd information

//generate indicators hospice use
gen r_hs_1d_6m = .
replace r_hs_1d_6m = 1 if hs_los_6m>=1 & hs_los_6m!=.
replace r_hs_1d_6m = 0 if hs_los_6m<1 & hs_los_6m!=.

gen r_hs_3d_6m = .
replace r_hs_3d_6m = 1 if hs_los_6m>=3 & hs_los_6m!=.
replace r_hs_3d_6m = 0 if hs_los_6m<3 & hs_los_6m!=.

gen r_hs_7d_6m = .
replace r_hs_7d_6m = 1 if hs_los_6m>=7 & hs_los_6m!=.
replace r_hs_7d_6m = 0 if hs_los_6m<7 & hs_los_6m!=.

la var r_hs_1d_6m "1+ days hospice use vs 0 days"
la var r_hs_3d_6m "3+ days hospice use vs 0-2 days"
la var r_hs_7d_6m "7+ days hospice use vs 0-6 days"

//hospice and intensive procedure use overlap?
gen hs_and_intens_pro = .
replace hs_and_intens_pro = 1 if r_hs_3d_6m==1 & int_any_of5_6m==1
replace hs_and_intens_pro = 0 if r_hs_3d_6m==0 | int_any_of5_6m==0
la var hs_and_intens_pro "Hospice 3+ days and Intensive Proced"

*********************************************************
save spouse_intens_data_sample.dta, replace
*********************************************************

la var int_intubation_6m "Intubation and mechanical ventilation"
la var int_trach_6m "Tracheostomy"
la var int_gastro_tude_6m "Gastrostomy tube insertion"
la var int_hemodia_6m "Hemodialysis"
la var int_enteral_nut_6m "Enteral or parenteral nutrition"
la var int_cpr_6m "CPR"

//first, look at intensive procedures in the sample before adding in sudden death excl
local intvars int_intubation_6m int_trach_6m int_gastro_tude_6m ///
int_hemodia_6m int_enteral_nut_6m int_cpr_6m int_any_6m int_any_of5_6m ///
r_hs_1d_6m r_hs_3d_6m r_hs_7d_6m hs_and_intens_pro 

local excl no_cc_12m_excl_cataract==0 & sudden_cause_death==0

mat int_n=(1,2,.)
sum r_exit_year_x if cesd_miss_n1_p1==0
mat int_n[1,1]=r(N)
sum r_exit_year_x if cesd_miss_n1_p1==0 & `excl'
mat int_n[1,2]=r(N)

mat rownames int_n="N"
frmttable, statmat(int_n) sdec(0) store(int1)

mat int_2=J(12,2,.)
local r = 1
foreach v in `intvars'{
	tab `v' if cesd_miss_n1_p1==0, missing	
	sum `v' if cesd_miss_n1_p1==0, detail
	mat int_2[`r',1]=r(mean)*100 //percent
	sum `v' if cesd_miss_n1_p1==0 & `excl', detail
	mat int_2[`r',2]=r(mean)*100 //percent
	local r = `r'+1
}

mat list int_2

mat rownames int_2=`intvars'

frmttable, varlabels statmat(int_2) sdec(2) store(int2)

//looking at hospice LOS
mat int_3 = J(2,4,.)

//row 1 = los overall sample
local r = 1
sum hs_los_6m if cesd_miss_n1_p1==0, detail
mat int_3[`r',1]=r(mean)
mat int_3[`r',2]=r(sd)
sum hs_los_6m if cesd_miss_n1_p1==0 & `excl', detail
mat int_3[`r',3]=r(mean)
mat int_3[`r',4]=r(sd)

//row 2 = los if 3+ day hospice use
local r = 2
sum hs_los_6m if cesd_miss_n1_p1==0 & r_hs_3d_6m==1, detail
mat int_3[`r',1]=r(mean)
mat int_3[`r',2]=r(sd)
sum hs_los_6m if cesd_miss_n1_p1==0 & `excl' & r_hs_3d_6m==1, detail
mat int_3[`r',3]=r(mean)
mat int_3[`r',4]=r(sd)

mat rownames int_3="Hospice LOS, mean (SD)" "LOS if 3+days use, mean (SD)"
mat list int_3

//save the table in frmttable	
mat dmat=(0,1,0,1)
mat ann=J(2,2,1)

frmttable, statmat(int_3) ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(int3) 	

outreg , replay(int1) append(int2) store(inta)

outreg using `logpath'\1-spouse_intens_pro_sample , ///
	replay(inta) append(int3) ///
	title("Intensive procedures") ///
	ctitle("","Full sample %" "Not sudden death") ///
	note("Decedents 2002-2010 exit interviews, with n1 core interview and ffs medicare" \ ///
	"last 6m of life, married at time of death with spouse with pre and post death core interview" \ ///
	"and pre and post death CES-D score complete." \ ///
	"Not sudden death excludes observations where cause of death = accidents, etc, or" \ ///
	"no comorb 12m before death excluding cataract and glaucoma." \ ///
	"Hospice use is based on claims in the last 6 months of R's life.") ///
	addtable
	
**************************************************
//cross tabulation of hospice use and intensive procedures
tab int_any_of5_6m r_hs_3d_6m if cesd_miss_n1_p1==0 , missing

mat xtab=J(5,4,.)
sum r_exit_year_x if cesd_miss_n1_p1==0 //sample c1
mat xtab[1,1] = r(N)
sum r_exit_year_x if cesd_miss_n1_p1==0 & `excl' //sample c2
mat xtab[1,3] = r(N)

sum r_exit_year_x if cesd_miss_n1_p1==0 & int_any_of5_6m==0 & r_hs_3d_6m==0
mat xtab[2,1]=r(N) //n
mat xtab[3,1]=r(N)/xtab[1,1]*100 //percent

sum r_exit_year_x if cesd_miss_n1_p1==0 & int_any_of5_6m==0 & r_hs_3d_6m==1
mat xtab[2,2]=r(N) //n
mat xtab[3,2]=r(N)/xtab[1,1]*100 //percent

sum r_exit_year_x if cesd_miss_n1_p1==0 & int_any_of5_6m==1 & r_hs_3d_6m==0
mat xtab[4,1]=r(N) //n
mat xtab[5,1]=r(N)/xtab[1,1]*100 //percent

sum r_exit_year_x if cesd_miss_n1_p1==0 & int_any_of5_6m==1 & r_hs_3d_6m==1
mat xtab[4,2]=r(N) //n
mat xtab[5,2]=r(N)/xtab[1,1]*100 //percent
**************************************************
sum r_exit_year_x if cesd_miss_n1_p1==0 & `excl' & int_any_of5_6m==0 & r_hs_3d_6m==0
mat xtab[2,3]=r(N) //n
mat xtab[3,3]=r(N)/xtab[1,3]*100 //percent

sum r_exit_year_x if cesd_miss_n1_p1==0 & `excl' & int_any_of5_6m==0 & r_hs_3d_6m==1
mat xtab[2,4]=r(N) //n
mat xtab[3,4]=r(N)/xtab[1,3]*100 //percent

sum r_exit_year_x if cesd_miss_n1_p1==0 & `excl' & int_any_of5_6m==1 & r_hs_3d_6m==0
mat xtab[4,3]=r(N) //n
mat xtab[5,3]=r(N)/xtab[1,3]*100 //percent

sum r_exit_year_x if cesd_miss_n1_p1==0 & `excl' & int_any_of5_6m==1 & r_hs_3d_6m==1
mat xtab[4,4]=r(N) //n
mat xtab[5,4]=r(N)/xtab[1,3]*100 //percent

mat list xtab

frmttable using `logpath'\1-spouse_intens_pro_sample , ///
	statmat(xtab) sdec(0\0\2\0\2) ///
	title("Crosstabulation of hospice use and intensive procedure use") ///
	ctitles("", "Full sample", "", "Not sudden death", "" \ ///
		"Hospice Use", "No", "Yes", "No", "Yes") ///
	rtitles("N overall" \ "Intensive procedure No, n" \ "%" \ "Yes, n"\ "%") ///
	note("Hospice use = 3+ days in last 6m of life") addtable


**************************************************
log close



H="Define new variables"
capture log close

clear all
set mem 500m
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\2-Spouse_intensive_variablesetup.txt", text replace

cd `datapath'

use spouse_intens_data_sample.dta
*************************************************
tab r_exit_year_x, missing

*************************************************
//demographic variables
sum r_age, detail
sum s_age, detail

//gender
tab r_female, missing
tab s_female, missing
//use spouse p1 interview to fill in missing gender
replace s_female=s_female_p1 if s_female==.
la var s_female "S Female"
la def fem 0 "Male" 1 "Female"
la val s_female r_female fem
tab s_female, missing

//race & ethnicity
tab r_hisp_eth_e, missing
tab r_white_e, missing
tab r_black_e, missing
tab r_other_na_api_race_e, missing

tab s_hisp_eth_e, missing
tab s_white_e, missing
tab s_black_e, missing
tab s_other_na_api_race_e, missing

//education
//code r-education variable using all interview information where
//there are no conflicts to try and avoid missingness when
//when use n1 and n2 core, still have lots of missing obs for 2010 decedents
//so brought in n3 core education variable to go back to 2004
gen r_educ = r_educ_n1
replace r_educ = r_educ_n2 if (r_educ_n1==.)
replace r_educ = r_educ_n3 if (r_educ_n1==. & r_educ_n2==.)
label variable r_educ "R's Eduction, Highest Level, imputed from all interviews"
label define r_educ 0 "No Formal Education" 1 "Grades 1-11" 2 "High School" 3 "Some College" 4 "College" ///
      5 "Post College", modify
label values r_educ r_educ 
tab r_educ, missing

//create r hs degree variable
gen byte r_hseduc_n1_n3 = .
replace r_hseduc_n1_n3=0 if inlist(r_educ,0,1)
replace r_hseduc_n1_n3=1 if inlist(r_educ,2,3,4,5)
la var r_hseduc_n1_n3 "HS Degree using n1-n3 core, 1=yes"
tab r_hseduc_n1_n3, missing

//code s-education variable using all interview information where
//there are no conflicts to try and avoid missingness when
//when use n1 and n2 core, still have lots of missing obs for 2010 decedents
//so brought in n3 core education variable to go back to 2004
gen s_educ = s_educ_n1
replace s_educ = s_educ_n2 if (s_educ_n1==.)
replace s_educ = s_educ_n3 if (s_educ_n1==. & s_educ_n2==.)
label variable s_educ "S's Eduction, Highest Level, imputed from all interviews"
label define s_educ 0 "No Formal Education" 1 "Grades 1-11" 2 "High School" 3 "Some College" 4 "College" ///
      5 "Post College", modify
label values s_educ s_educ 
tab s_educ, missing

//create s hs degree variable
gen byte s_hseduc_n1_n3 = .
replace s_hseduc_n1_n3=0 if inlist(s_educ,0,1)
replace s_hseduc_n1_n3=1 if inlist(s_educ,2,3,4,5)
la var s_hseduc_n1_n3 "S HS Degree using n1-n3 core, 1=yes"
la def hs 0 "No HS degree" 1 "HS Degree"
la val s_hseduc_n1_n3 r_hseduc_n1_n3 hs
tab s_hseduc_n1_n3, missing

//marital status
tab r_married_x, missing

*************************************************
//additional respondant variables
tab r_nw_lowest_n1, missing
tab r_nw_midlow_n1, missing
tab r_nw_midhigh_n1, missing 
tab r_nw_highest_n1, missing

tab r_imprelig_vimp_n1, missing
tab s_imprelig_vimp_n1, missing

//adl indicator for needing assistance
gen s_adl_impair_n1 = 0 if  s_adl_independent_core_n1 == 1
replace s_adl_impair_n1 = 1 if s_adl_independent_core_n1 == 0
la var s_adl_impair_n1 "S ADL impairment n1"
tab s_adl_impair_n1 s_adl_independent_core_n1, missing

gen r_adl_impair_n1 = 0 if  r_adl_independent_core_n1 == 1
replace r_adl_impair_n1 = 1 if r_adl_independent_core_n1 == 0
la var r_adl_impair_n1 "R ADL impairment n1"
tab r_adl_impair_n1 r_adl_independent_core_n1, missing

gen r_adl_impair_n2 = 0 if  r_adl_independent_core_n2 == 1
replace r_adl_impair_n2 = 1 if r_adl_independent_core_n2 == 0
la var r_adl_impair_n2 "R ADL impairment n2"
tab r_adl_impair_n2 r_adl_independent_core_n2, missing

//insurance status
tab r_medicaid_n1 s_medicaid_n1, missing
tab r_medigap_n1 s_medigap_n1, missing
tab r_champus_n1 s_champus_n1, missing

//eol planning
tab r_advdir_x, missing
tab r_discuss_x, missing

//chronic conditions count, excluding glaucoma and cataract
gen cc_all_count_6m12m_2 = cc_1_ami_n12mn6m + cc_2_alzh_n12mn6m + cc_3_alzhdmta_n12mn6m + ///
cc_4_atrialfb_n12mn6m + /*cc_5_cataract_n12mn6m +*/ cc_6_chrnkidn_n12mn6m + ///
cc_7_copd_n12mn6m + cc_8_chf_n12mn6m + cc_9_diabetes_n12mn6m + /*cc_10m_glaucoma_n12mn6m +*/ ///
cc_11_hipfrac_n12mn6m + cc_12_ischmcht_n12mn6m + cc_13_depressn_n12mn6m + ///
cc_14_osteoprs_n12mn6m + cc_15_ra_oa_n12mn6m + cc_16_strketia_n12mn6m + ///
cc_17_cncrbrst_n12mn6m + cc_18_cncrclrc_n12mn6m + cc_19_cncrprst_n12mn6m + ///
cc_20_cncrlung_n12mn6m + cc_21_cncrendm_n12mn6m
la var cc_all_count_6m12m_2 "Count of cc's, 6-12m pre-death, excludes cataract and glaucoma"
tab cc_all_count_6m12m_2 , missing

//categorical spouse primary helper, duration'
tab r_adl_sp_helper_x r_iadl_sp_helper_x, missing
gen r_s_help_dur_cat = 0 if r_adl_sp_helper_x==0 & r_iadl_sp_helper_x==0
replace r_s_help_dur_cat = 1 if r_adl_sp_helper_x==1 | r_iadl_sp_helper_x==1
replace r_s_help_dur_cat = 2 if (r_adl_sp_helper_x==1 | r_iadl_sp_helper_x==1) ///
 & ( r_adl_sp_helper_n1==1 | r_iadl_sp_helper_n1==1)
la def help 0 "S not primary helper x" 1 "S primary helper x only" 2 "S primary helper x+n1"
la val r_s_help_dur_cat help
tab r_s_help_dur_cat, missing

tab r_s_help_dur_cat, gen(r_s_help_dur_ind)

la var r_s_help_dur_ind1 "S not prim helper exit"
la var r_s_help_dur_ind2 "S prim helper exit only"
la var r_s_help_dur_ind3 "S prim helper exit and n1"

*************************************************
//hospice , intensive procedures timing variables
gen proc_to_hs_enrol = hs_enrol_dt - first_proc_date_excl_hd_6m 
tab proc_to_hs_enrol, missing

gen hs_enrol_to_death = r_death_date - hs_enrol_dt
tab hs_enrol_to_death , missing
tab hs_los_6m if hs_enrol_to_death>180 & hs_enrol_to_death~=.

*************************************************
//interview timeline variables
//note these use the dod from HRS if present and claims if HRS is missing
sum r_days_n1_core_dead, detail
sum s_days_n1_core_dead, detail

gen r_days_n2_core_dead = r_death_date-r_c_ivw_date_n2
gen s_days_n2_core_dead = r_death_date-s_c_ivw_date_n2
label var r_days_n2_core_dead "Days from R's n2 ivw to death"
label var s_days_n2_core_dead "Days from S's n2 ivw to R's death"

gen r_days_x_exit_dead = r_e_ivw_date_x - r_death_date
label var days_p1_core_dead "Days from R's death to S p1 interview"
sum r_days_x_exit_dead, detail
sum days_p1_core_dead, detail

//create categorical variable for 6m intervals for death to p1 ivw
//categorical variable for interview timing from r's death
gen days_p1_core_dod_cat = .
replace days_p1_core_dod_cat = 0 if 0<=days_p1_core_dead & days_p1_core_dead<183
replace days_p1_core_dod_cat = 1 if 183<=days_p1_core_dead & days_p1_core_dead<366
replace days_p1_core_dod_cat = 2 if 366<=days_p1_core_dead & days_p1_core_dead<548
replace days_p1_core_dod_cat = 3 if 548<=days_p1_core_dead & days_p1_core_dead<731
replace days_p1_core_dod_cat = 4 if 731<=days_p1_core_dead & days_p1_core_dead !=.
la def days_p1_core_dod_cat 0 "0-6 months" 1 "6-12 months" 2 "12-18 months" ///
3 "18-24 months" 4 "24+ months"
la var days_p1_core_dod_cat "Days from R death to p1 interview, categorical"
la val days_p1_core_dod_cat days_p1_core_dod_cat 
tab days_p1_core_dod_cat , missing

tab days_p1_core_dod_cat , gen(p1_ind)
rename p1_ind1 days_p1_core_dod_lt6m 
rename p1_ind2 days_p1_core_dod_6_12m 
rename p1_ind3 days_p1_core_dod_12_18m 
rename p1_ind4 days_p1_core_dod_18_24m 
rename p1_ind5 days_p1_core_dod_gt2yr 

la var days_p1_core_dod_lt6m "R death to P1 ivw lt 6m"
la var days_p1_core_dod_6_12m "R death to P1 ivw 6-12m"
la var days_p1_core_dod_12_18m "R death to P1 ivw 12-18m"
la var days_p1_core_dod_18_24m "R death to P1 ivw 18-24m"
la var days_p1_core_dod_gt2yr "R death to P1 ivw gt 2yr"

foreach v in days_p1_core_dod_lt6m days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr{
	sum days_p1_core_dead if `v'==1, detail
}

tab r_ind_n1_wi_6m_dead, missing
tab s_ind_n1_wi_6m_dead, missing

*************************************************
//variables that use n2 core if r n1 within 6m of death
local rvarn1n2 r_nhres r_rel_nb r_adl_impair r_srh_pf 
local svarn1n2 s_psych_overall_hrs s_cesd_tot_ge3 s_srh_pf s_adl_independent_core
//note s_cesd_tot_n1n2 created in previous section

foreach i in r s{
	foreach v in ``i'varn1n2'{
		tab `v'_n1 `i'_ind_n1_wi_6m_dead, missing
		gen `v'_n1n2 = `v'_n1 if `i'_ind_n1_wi_6m_dead==0
		replace `v'_n1n2 = `v'_n2 if `i'_ind_n1_wi_6m_dead==1
		tab `v'_n1n2, missing
	}
}

la var r_nhres_n1n2 "R NH Res at n1 or n2 core"
la var r_rel_nb_n1n2 "R Relatives nearby at n1 or n2 core"
la var r_adl_impair_n1n2 "R ADL Impaired at n1 or n2 core"
la var r_srh_pf_n1n2 "R SRH Fair/Poor n1 or n2 core"

la var s_psych_overall_hrs_n1n2 "S Psych condition n1 or n2 core"
la var s_cesd_tot_n1n2 "S CES-D score, n1 or n2 core"
la var s_cesd_tot_ge3_n1n2 "S Depressed CESD 3+, n1 or n2 core"
la var s_srh_pf_n1n2 "S SRH Poor/Fair, n1 or n2 core"
la var s_adl_independent_core_n1n2 "S ADL Independent, n1 or n2 core"
*************************************************
//s comorb category indicators
tab s_comor_c_hrs_n1, missing
tab s_comor_c_hrs_n1, gen(scom)
rename scom1 s_hrs_comorb_none_n1
rename scom2 s_hrs_comorb_mild_n1
rename scom3 s_hrs_comorb_mod_n1
rename scom4 s_hrs_comorb_sev_n1

tab s_hrs_comorb_none_n1, missing
tab s_hrs_comorb_mild_n1, missing
tab s_hrs_comorb_mod_n1, missing
tab s_hrs_comorb_sev_n1, missing

//since only 1 obs has severe, categorize as mod
replace s_hrs_comorb_mod_n1=1 if s_hrs_comorb_sev_n1==1

*************************************************
//s cesd change variables from interview before 6m before R's death to p1 interview

//change cesd count n1 to p1?
gen s_cesdchange= s_cesd_tot_p1- s_cesd_tot_n1n2
tab s_cesdchange, missing
la var s_cesdchange "Change in S's CESD count n1 to p1"
*histogram s_cesdchange

//CESD improvement indicator
gen byte s_cesdbetter=.
replace s_cesdbetter= 1 if s_cesdchange <0
replace s_cesdbetter= 0 if (s_cesdchange >=0 & s_cesdchange !=.)
la var s_cesdbetter "S CESD Improvement n1 to p1"

//CESD decline indicator
gen byte s_cesdworse=.
replace s_cesdworse= 1 if (s_cesdchange >0 & s_cesdchange !=.)
replace s_cesdworse= 0 if s_cesdchange <=0 
la var s_cesdworse"S CESD Decline n1 to p1"

//CESD decline indicator
gen byte s_cesdsame=.
replace s_cesdsame= 1 if s_cesdchange ==0
replace s_cesdsame= 0 if (s_cesdchange !=0 & s_cesdchange !=.)
la var s_cesdsame "S CESD Stable n1 to p1"

tab s_cesdchange s_cesdbetter, missing
tab s_cesdchange s_cesdworse, missing
tab s_cesdchange s_cesdsame , missing

//cesd change categorical variable
gen s_cesdchange_3 =.
replace s_cesdchange_3 =1 if (s_cesdchange <0 & s_cesdchange!=.) // improvement
replace s_cesdchange_3 =-1 if (s_cesdchange >0 & s_cesdchange!=.) //decline
replace s_cesdchange_3 =0 if (s_cesdchange ==0) //no change
la var s_cesdchange_3 "S CESD change categories n1 to p1"
la def s_cesdchange_3 1 "Improvement" 0 "No change" -1 "Decline"
la val s_cesdchange_3 s_cesdchange_3 
tab s_cesdchange_3, missing

//cesd improved from >=3 to 3-  look at CESD 3 clin depression change 
//(three options compared to improved or not*
gen s_cesd_ge3_change= s_cesd_tot_ge3_p1- s_cesd_tot_ge3_n1n2

//cesd improved: 3 or higher in n1 to less than 3 in p1
gen byte s_cesd_ge3better = .
replace s_cesd_ge3better = 1 if s_cesd_ge3_change < 0
replace s_cesd_ge3better = 0 if (s_cesd_ge3_change >= 0 & s_cesd_ge3_change !=.)
la var s_cesd_ge3better "S CESD change from 3+ n1 to <3 p1"
tab s_cesd_ge3better s_cesd_ge3_change, missing

//cesd same or better: 2 or higher in n1 to less than 3 in p1 or no change
gen byte s_cesd_ge3betterorsame = .
replace s_cesd_ge3betterorsame = 1 if s_cesd_ge3_change<=0
replace s_cesd_ge3betterorsame = 0 if (s_cesd_ge3_change > 0 & s_cesd_ge3_change !=.)
la var s_cesd_ge3betterorsame "S CESD no change or change from 3+ n1 to <3 p1"
tab s_cesd_ge3betterorsame s_cesd_ge3_change, missing
*************************************************
//spouse primary caregiver per exit helper details?
tab r_adl_sp_helper_x r_iadl_sp_helper_x, missing

*************************************************
//regional variables
sum hci_index, detail
sum hospital_beds, detail
sum specialists, detail

*************************************************
save spouse_intens_data_cleaned.dta, replace

*************************************************
log close


H="Univariate comparison intensive procedures vs not"
/*First look at differences between intensive procedure and not groups
univariate comparison using p-values
first version had standardized deviation, those sections are commented out for now
in case we want to go back to stand dev when we do matching

Only compare those with full cesd pre/post
Does include those without sudden death
*/

capture log close

clear all
set mem 500m
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\3-Spouse_intensive_univariate.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_cleaned.dta if cesd_miss_n1_p1==0
*********************************************************

tab int_any_of5_6m, missing 
*********************************************************
//Table 1 Decedent characteristics
*********************************************************
local rvars r_female r_black_e r_hisp_eth_e r_white_e r_hseduc_n1_n3 r_nw_lowest_n1 ///
r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 r_medicaid_n1 r_champus_n1 r_medigap_n1 ///
r_imprelig_vimp_n1 r_rel_nb_n1n2 r_nhres_n1n2 r_advdir_x r_discuss_x r_hs_3d_6m ///
r_adl_impair_n1n2 r_srh_pf_n1n2 ///
cc_3_alzhdmta_n12mn6m ///
cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m cc_8_chf_n12mn6m ///
cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m cc_16_strketia_n12mn6m ///
cc_cncr_chronic_n12mn6m cc_4_atrialfb_n12mn6m cc_11_hipfrac_n12mn6m ///
cc_13_depressn_n12mn6m cc_14_osteoprs_n12mn6m cc_15_ra_oa_n12mn6m 

local rvars_2 cc_all_count_6m12m_2 hci_index hospital_beds specialists

la var r_age "Age at death, years, mean (SD)"
la var r_female "Female, %"
la var r_black_e "Race, Non-Hispanic, Black, %*"
la var r_hisp_eth_e "Hispanic Ethnicity, %*"
la var r_white_e "White, %*"
la var r_hseduc_n1_n3 "Education, High School Grad, %"
la var r_nw_lowest_n1 "Net Worth, 1 (low) Quartile, %"
la var r_nw_midlow_n1  "Net Worth, 2 Quartile, %"
la var r_nw_midhigh_n1  "Net Worth, 3 Quartile, %"
la var r_nw_highest_n1  "Net Worth, 4 (high) Quartile, %"
la var r_medicaid_n1  "Medicaid, %"
la var r_champus_n1  "VA Insurance, %"
la var r_medigap_n1 "Medigap, %"
la var r_imprelig_vimp_n1 "Religion very important, %"
la var r_rel_nb_n1n2 "Relatives nearby, %**"
la var r_nhres_n1n2 "Nursing home resident pre-death, %**"
la var r_advdir_x "Had advanced directive, %**"
la var r_discuss_x "Discussion of EOL care, %"
la var r_hs_3d_6m "Hospice 3+ days in last 6m of life, %"
la var r_adl_impair_n1n2 "ADL Impairment, %**"
la var r_srh_pf_n1n2 "SRH poor or fair, %**"
la var cc_all_count_6m12m_2 "Number of chronic conditions, mean (SD)"
la var hci_index "Hospital care intensity index, mean (SD)"
la var hospital_beds "Acute care hospital beds/1000 res., mean (SD)"
la var specialists "Specialists/10000 res., mean (SD)"

//part 1, get n's
mat t1_n=J(1,3,.)
tab int_any_of5_6m, missing matcell(d1)
mat t1_n[1,1]=d1[2,1] //no intensive procedures n
mat t1_n[1,2]=d1[1,1] //intensive procedures n
mat rownames t1_n="Sample n"

frmttable, statmat(t1_n) varlabels ///
	sdec(0) store(tab1_1)

//part 2, get age mean, std deviation, p-value
mat t1_2=J(1,5,.)
local c = 1
foreach i in 1 0{
	sum r_age if int_any_of5_6m==`i'
	mat t1_2[1,`c']=r(mean)
	mat t1_2[1,`c'+1]=r(sd)
	local c = `c'+2	
}
local r = 1
//mat t1_2[`r',5]=(t1_2[`r',1]-t1_2[`r',3])/sqrt((t1_2[`r',2]^2+t1_2[`r',4]^2)/2) 
ttest r_age, by(int_any_of5_6m) 
mat t1_2[`r',5]=r(p)

mat rownames t1_2=r_age

mat dmat=(0,1,0,1,0)
mat ann=(1,1,0\1,1,0)
mat list dmat
mat list ann

mat list t1_2

frmttable, statmat(t1_2) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1_2) 	

//part 3, r characteristics, binary vars, % and std diff
mat t1_3=J(33,3,.)
local r = 1
foreach v in `rvars'{
	local c = 1
	foreach i in 1 0{
		sum `v' if int_any_of5_6m==`i'
		mat t1_3[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
//	mat t1_3[`r',3]=(t1_3[`r',1]/100-t1_3[`r',2]/100)/sqrt((t1_3[`r',1]/100* ///
//		(1-t1_3[`r',1]/100)+t1_3[`r',2]/100*(1-t1_3[`r',2]/100))/2)
	tab int_any_of5_6m `v', row chi
	mat t1_3[`r',3]=r(p)
	local r = `r'+1
}
mat rownames t1_3=`rvars'
frmttable, statmat(t1_3) varlabels ///
	sdec(2) store(tab1_3)

//part 4, remaining continuous variables, mean (sd)
mat t1_4=J(4,5,.)
local r = 1
foreach v in `rvars_2'{
	local c = 1
	foreach i in 1 0{
		sum `v' if int_any_of5_6m==`i'
		mat t1_4[`r',`c']=r(mean)
		mat t1_4[`r',`c'+1]=r(sd)
		local c = `c'+2	
	}
//	mat t1_4[`r',5]=(t1_4[`r',1]-t1_4[`r',3])/sqrt((t1_4[`r',2]^2+t1_4[`r',4]^2)/2) 
	ttest `v', by(int_any_of5_6m)
	mat t1_4[`r',5]=r(p)
	local r = `r'+1
}
mat rownames t1_4=`rvars_2'

mat list t1_4

mat dmat=(0,1,0,1,0)
mat ann=(1,1,0\1,1,0\1,1,0\1,1,0)
mat list dmat
mat list ann

frmttable, statmat(t1_4) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1_4) 	

//combine into single table and export
outreg, replay(tab1_1) append(tab1_2) store(tab1_a)
outreg, replay(tab1_a) append(tab1_3) store(tab1_b)
outreg using `logpath'/3_s_intens_pro_univar, ///
	replay(tab1_b) append(tab1_4) ///
	title("Table 1: Decedent and region characteristics by intensive procedure use") ///
	ctitles("","Intensive Procedure Used","No intensive procedure","P-value") ///
	note("* Categories do not total 100% because of missing responses" \ ///
	"** If pre-death interview within 6m of death, use prior interview (n2) response" \ ///
	"Chronic conditions are cms data warehouse chronic conditions, 6-12 pre death"\ ///
	"P-value from t-test for continuous variables and chi-squared test for categorical variables.") ///
	replace

*********************************************************
//Table 2 Spouse characteristics
*********************************************************
local svars s_female s_black_e s_hisp_eth_e s_white_e s_hseduc_n1_n3 ///
s_srh_pf_n1n2 s_imprelig_vimp_n1 s_hrs_comorb_none_n1 ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
s_cesdbetter s_cesd_ge3better s_cesd_tot_ge3_n1n2 s_cesd_tot_ge3_p1 ///
days_p1_core_dod_lt6m days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr

gen months_p1_core_dead = days_p1_core_dead /30.5

local svars_2 months_p1_core_dead s_cesdchange s_cesd_tot_n1n2 s_cesd_tot_p1 

la var s_age "Age at  R's death, years, mean (SD)"
la var s_female "Female, %"
la var s_black_e "Race, Non-Hispanic, Black, %*"
la var s_hisp_eth_e "Hispanic Ethnicity, %*"
la var s_white_e "White, %*"
la var s_hseduc_n1_n3 "Education, High School Grad, %"

la var s_srh_pf_n1n2 "SRH Fair or Poor, %**"
la var s_imprelig_vimp_n1 "Religion very important, %" 
la var s_hrs_comorb_none_n1 "Comorbidities - none, %"
la var s_hrs_comorb_mild_n1 "Mild (1-3), %"
la var s_hrs_comorb_mod_n1 "Moderate (4-6) or Severe (7), %"
la var s_psych_overall_hrs_n1n2 "Psychiatric condition, %**"
la var s_cesdbetter "CESD Improved (any amount) pre to post death, %"
la var s_cesd_ge3better "CESD Clinical Improvement pre to post death, %"
la var s_cesd_tot_ge3_n1n2 "Clinically depressed (3+) pre death, %"
la var s_cesd_tot_ge3_p1 "Clinically depressed (3+) post death, %"
la var days_p1_core_dod_lt6m "P1 ivw 0-6m, %"
la var days_p1_core_dod_6_12m "P1 ivw 6-12m, %"
la var days_p1_core_dod_12_18m "P1 ivw 12-18m, %"
la var days_p1_core_dod_18_24m "P1 ivw 18-24m, %"
la var days_p1_core_dod_gt2yr "P1 ivw 2+ years, %"

la var months_p1_core_dead "Months R's death to p1 ivw, mean (SD)"
la var s_cesdchange "S CES-D score change, pre to post, mean (SD)"
la var s_cesd_tot_n1n2 "S CES-D score, pre-death, mean (SD)"
la var s_cesd_tot_p1 "S CES-D score, post-death, mean (SD)"

//part 1, get n's
//just use tab1_1 from Decedent char table above

//part 2, get age mean, std deviation, p-value
mat t2_2=J(1,5,.)
local c = 1
foreach i in 1 0{
	sum s_age if int_any_of5_6m==`i'
	mat t2_2[1,`c']=r(mean)
	mat t2_2[1,`c'+1]=r(sd)
	local c = `c'+2	
}
local r = 1
//mat t2_2[`r',5]=(t2_2[`r',1]-t2_2[`r',3])/sqrt((t2_2[`r',2]^2+t2_2[`r',4]^2)/2) 
ttest s_age, by(int_any_of5_6m)
mat t2_2[`r',5]=r(p)

mat rownames t2_2=s_age

mat dmat=(0,1,0,1,0)
mat ann=(1,1,0\1,1,0)
mat list dmat
mat list ann

mat list t2_2

frmttable, statmat(t2_2) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_2) 	

//part 3, s characteristics, binary vars, % and std diff
mat t2_3=J(20,3,.)
local r = 1
foreach v in `svars'{
	local c = 1
	foreach i in 1 0{
		sum `v' if int_any_of5_6m==`i'
		mat t2_3[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
//	mat t2_3[`r',3]=(t2_3[`r',1]/100-t2_3[`r',2]/100)/sqrt((t2_3[`r',1]/100* ///
//		(1-t2_3[`r',1]/100)+t2_3[`r',2]/100*(1-t2_3[`r',2]/100))/2)
	tab int_any_of5_6m `v', row chi
	mat t2_3[`r',3]=r(p)
	local r = `r'+1
}
mat rownames t2_3=`svars'
mat list t2_3
frmttable, statmat(t2_3) varlabels ///
	sdec(2) store(tab2_3)

//part 4, remaining continuous variables, mean (sd)
mat t2_4=J(4,5,.)
local r = 1
foreach v in `svars_2'{
	local c = 1
	foreach i in 1 0{
		sum `v' if int_any_of5_6m==`i'
		mat t2_4[`r',`c']=r(mean)
		mat t2_4[`r',`c'+1]=r(sd)
		local c = `c'+2	
	}
//	mat t2_4[`r',5]=(t2_4[`r',1]-t2_4[`r',3])/sqrt((t2_4[`r',2]^2+t2_4[`r',4]^2)/2) 
	ttest `v', by(int_any_of5_6m)
	mat t2_4[`r',5]=r(p)
	local r = `r'+1
}
mat rownames t2_4=`svars_2'

mat list t2_4

frmttable, statmat(t2_4) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_4) 	

//combine into single table and export
outreg, replay(tab1_1) append(tab2_2) store(tab2_a)
outreg, replay(tab2_a) append(tab2_3) store(tab2_b)
outreg using `logpath'/3_s_intens_pro_univar, ///
	replay(tab2_b) append(tab2_4) ///
	title("Table 2: Spouse characteristics by intensive procedure use") ///
	ctitles("","Intensive Procedure Used","No intensive procedure","P-value") ///
	note("* Categories do not total 100% because of missing responses" \ ///
	"** If pre-death interview within 6m of death, use prior interview (n2) response" \ ///
	"P-value from t-test for continuous variables and chi-squared test for categorical variables.") ///
	addtable

*********************************************************
log close


H="Sample characteristics table"
/*Overall sample characteristics

Only compare those with full cesd pre/post
Does include those without sudden death
*/

capture log close

clear all
set mem 500m
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\4-Spouse_intensive_overall_sample.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_cleaned.dta if cesd_miss_n1_p1==0
**************************************************************
local rvars r_female r_black_e r_hisp_eth_e r_white_e r_hseduc_n1_n3 ///
r_medicaid_n1 r_champus_n1 r_medigap_n1 ///
r_imprelig_vimp_n1 r_rel_nb_n1n2 r_nhres_n1n2 r_advdir_x r_discuss_x ///
r_hs_3d_6m  r_adl_impair_n1n2 r_srh_pf_n1n2 ///
cc_3_alzhdmta_n12mn6m ///
cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m cc_8_chf_n12mn6m ///
cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m cc_16_strketia_n12mn6m ///
cc_cncr_chronic_n12mn6m cc_4_atrialfb_n12mn6m cc_11_hipfrac_n12mn6m ///
cc_13_depressn_n12mn6m cc_14_osteoprs_n12mn6m cc_15_ra_oa_n12mn6m 

local rvars_2 cc_all_count_6m12m_2 hci_index hospital_beds specialists s_age

la var r_age "Age at death, years, mean (SD)"
la var r_networth_adj2010_n1 "Net worth, mean (median)"
la var r_female "Female, %"
la var r_black_e "Race, Non-Hispanic, Black, %*"
la var r_hisp_eth_e "Hispanic Ethnicity, %*"
la var r_white_e "White, %*"
la var r_hseduc_n1_n3 "Education, High School Grad, %"
la var r_medicaid_n1  "Medicaid, %"
la var r_champus_n1  "VA Insurance, %"
la var r_medigap_n1 "Medigap, %"
la var r_imprelig_vimp_n1 "Religion very important, %"
la var r_rel_nb_n1n2 "Relatives nearby, %**"
la var r_nhres_n1n2 "Nursing home resident pre-death, %**"
la var r_advdir_x "Had advanced directive, %**"
la var r_discuss_x "Discussion of EOL care, %"
la var r_hs_3d_6m "Hospice 3+ days in last 6m of life, %"
la var r_adl_impair_n1n2 "ADL Impairment, %**"
la var r_srh_pf_n1n2 "SRH poor or fair, %**"
la var cc_all_count_6m12m_2 "Number of chronic conditions, mean (SD)"
la var hci_index "Hospital care intensity index, mean (SD)"
la var hospital_beds "Acute care hospital beds/1000 res., mean (SD)"
la var specialists "Specialists/10000 res., mean (SD)"

local svars s_female s_black_e s_hisp_eth_e s_white_e s_hseduc_n1_n3 ///
s_srh_pf_n1n2 s_imprelig_vimp_n1 s_hrs_comorb_none_n1 ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
s_cesdbetter s_cesd_ge3better s_cesd_tot_ge3_n1n2 s_cesd_tot_ge3_p1 

local svars_2 s_cesd_tot_n1n2 s_cesd_tot_p1 

la var s_age "Age at  R's death, years, mean (SD)"
la var s_female "Female, %"
la var s_black_e "Race, Non-Hispanic, Black, %*"
la var s_hisp_eth_e "Hispanic Ethnicity, %*"
la var s_white_e "White, %*"
la var s_hseduc_n1_n3 "Education, High School Grad, %"

la var s_srh_pf_n1n2 "SRH Fair or Poor, %**"
la var s_imprelig_vimp_n1 "Religion very important, %" 
la var s_hrs_comorb_none_n1 "Comorbidities - none, %"
la var s_hrs_comorb_mild_n1 "Mild (1-3), %"
la var s_hrs_comorb_mod_n1 "Moderate (4-6) or Severe (7), %"
la var s_psych_overall_hrs_n1n2 "Psychiatric condition, %**"
la var s_cesdbetter "CESD Improved (any amount) pre to post death, %"
la var s_cesd_ge3better "CESD Clinical Improvement pre to post death, %"
la var s_cesd_tot_ge3_n1n2 "Clinically depressed (3+) pre death, %"
la var s_cesd_tot_ge3_p1 "Clinically depressed (3+) post death, %"

la var s_cesd_tot_n1n2 "S CES-D score, pre-death, mean (SD)"
la var s_cesd_tot_p1 "S CES-D score, post-death, mean (SD)"
**************************************************************
//create table

mat tab1=J(1,2,.)

sum r_age, detail //r age
mat tab1[1,1]=r(mean)
mat tab1[1,2]=r(sd)

mat rownames tab1=r_age

mat dmat=(0,1)
mat ann=(1)

mat list tab1

frmttable, statmat(tab1) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1) 	

sum r_networth_adj2010_n1, detail //r net worth
mat tab1[1,1]=r(mean)
mat tab1[1,2]=r(p50)
mat rownames tab1=r_networth_adj2010_n1
frmttable, statmat(tab1) varlabels ///
	sdec(0) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	append(tab1) store(tab1)
	
foreach v in `rvars'{
sum `v'
mat tab1[1,1]=r(mean)*r(N) //n
mat tab1[1,2]=r(mean)*100 //percent
mat rownames tab1=`v'

frmttable, statmat(tab1) varlabels ///
	sdec(0) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	append(tab1) store(tab1)
}	

foreach v in `rvars_2'{
sum `v'
mat tab1[1,1]=r(mean) //mean
mat tab1[1,2]=r(sd) //sd
mat rownames tab1=`v'

frmttable, statmat(tab1) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	append(tab1) store(tab1)
}

foreach v in `svars'{
sum `v'
mat tab1[1,1]=r(mean)*r(N) //n
mat tab1[1,2]=r(mean)*100 //percent
mat rownames tab1=`v'

frmttable, statmat(tab1) varlabels ///
	sdec(0) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	append(tab1) store(tab1)
}

foreach v in `svars_2'{
sum `v'
mat tab1[1,1]=r(mean) //mean
mat tab1[1,2]=r(sd) //sd
mat rownames tab1=`v'

frmttable, statmat(tab1) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	append(tab1) store(tab1)
}

//also generate the overall n
mat tab1_n=J(1,1,.)
sum r_exit_year_x
mat tab1_n[1,1]=r(N)

mat rownames tab1_n="N"
frmttable, statmat(tab1_n) sdec(0) store(tab1_n)

outreg using "`logpath'/4_overall_sample_char" , ///
replay(tab1_n) append(tab1) replace ///
title("Spouse intensive procedures - Overall sample characteristics") ///
	note("* Categories do not total 100% because of missing responses" \ ///
	"** If pre-death interview within 6m of death, use prior interview (n2) response")

**************************************************************
log close


H="Run multivariate logit regression"
/*Multivariate regression on spousal depression including intensive procedure use

Only compare those with full cesd pre/post
Does include those without sudden death

3 outcomes modeled:
1. Any S CES-D improvement (binary)
2. S CES-D clincial improvement from 3+ before death to less than 3 after death (binary)
3. S CES-D change pre to post death (continuous)

Run 2 versions, one with all dyads, one excluding hospice users

*/

capture log close

clear all
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\5-Spouse_intensive_multivar.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_cleaned.dta if cesd_miss_n1_p1==0
*******************************************************************
la var int_any_of5_6m "Intensive procedure last 6 months of life"
la var r_hseduc_n1_n3 "R HS Degree"
la var r_nhres_n1n2 "Nursing home resident"
la var r_adl_impair_n1n2 "ADL Impairment"
la var cc_cncr_chronic_n12mn6m "Number of Chronic Conditions"
la var s_srh_pf_n1n2 "S SRH Poor or Fair"
la var s_hrs_comorb_mild_n1 "S Comorb. Mild 1-3"
la var s_hrs_comorb_mod_n1 "S Comorb. Mod 4-6 or Severe 7"
la var s_psych_overall_hrs_n1n2 "S Psychiatric condition"
la var hci_index "Hospital care intensity index"
la var hospital_beds "Acute care hospital beds/1000 res."
la var specialists "Specialists per 100000 res"

la var r_hs_3d_6m "Hospice 3+ days in last 6m of life"

//where r&s highly correlated, use r
local rvars r_age r_female r_white_e ///
r_hseduc_n1_n3 r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 ///
r_nhres_n1n2 r_advdir_x r_hs_3d_6m r_adl_impair_n1n2 cc_3_alzhdmta_n12mn6m ///
cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m cc_8_chf_n12mn6m ///
cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m cc_16_strketia_n12mn6m ///
cc_cncr_chronic_n12mn6m cc_15_ra_oa_n12mn6m cc_all_count_6m12m_2

local region hci_index hospital_beds specialists

local svars s_srh_pf_n1n2 s_imprelig_vimp_n1  ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
 days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr
*******************************************************************
//check correlation
pwcorr int_any_of5_6m `rvars' `svars' `region'

*******************************************************************
//base categories - R=not white, low quartile net worth, S no comorbidities, 
//S p1 ivw 0-6m after R's death
logit s_cesdbetter int_any_of5_6m `rvars' `svars' `region', or vce(cluster hrrnum)
outreg , ///
	title("Impact of intensive procedure on Spousal depression") ///
	varlabels stats(e_b e_ci p) nosubstat ///
	ctitles("" "(1) Any improvement" "" "" \ "" "OR" "95% CI" "P-value") ///
	note("Base categories are lowest net worth quartile," \ ///
	"spouse no comorbidities per HRS survey," \ ///
	"and S p1 interview 0-6m after R's death." \ ///
	"Robust standard errors are clustered by HRR.") ///
	starlevels(10 5 1) store(res1) summstat(N) landscape


logit s_cesd_ge3better int_any_of5_6m `rvars' `svars' `region', or vce(cluster hrrnum)
outreg using `logpath'\5_multivar_logit_results, ///
	varlabels stats(e_b e_ci p) nosubstat ///
	ctitles("" "(2) Clinical improvement" "" "" \ "" "OR" "95% CI" "P-value") ///
	starlevels(10 5 1) merge(res1) summstat(N) landscape replace

regress s_cesdchange int_any_of5_6m `rvars' `svars' `region', vce(cluster hrrnum)
outreg using `logpath'\5_multivar_logit_results, ///
	varlabels stats(e_b e_ci p) nosubstat ///
	title("Outcome = Change in CES-D score, pre to post death") ///
	ctitles("" "Coefficient" "95% CI" "P-value") ///
		note("Base categories are lowest net worth quartile," \ ///
	"spouse no comorbidities per HRS survey," \ ///
	"and S p1 interview 0-6m after R's death." \ ///
	"CES-D possible range 0-8, Change CES-D range = -8 to +8" \ ///
	"Positive change indicates higher CES-D post death, ie more depressed." \ ///
	"Robust standard errors are clustered by HRR.") ///
	starlevels(10 5 1) summstat(N) landscape addtable

*******************************************************************
//run version excluding those with 3+ days hospice use last 6mos
local rvars2 r_age r_female r_white_e ///
r_hseduc_n1_n3 r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 ///
r_nhres_n1n2 r_advdir_x r_adl_impair_n1n2 cc_3_alzhdmta_n12mn6m ///
cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m cc_8_chf_n12mn6m ///
cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m cc_16_strketia_n12mn6m ///
cc_cncr_chronic_n12mn6m cc_15_ra_oa_n12mn6m cc_all_count_6m12m_2

logit s_cesdbetter int_any_of5_6m `rvars2' `svars' `region' if r_hs_3d_6m==0, or vce(cluster hrrnum)
outreg , ///
	title("Impact of intensive procedure on Spousal depression" \ ///
	"Excluding observations where R hospice use 3+ days") ///
	varlabels stats(e_b e_ci p) nosubstat ///
	ctitles("" "(1) Any improvement" "" "" \ "" "OR" "95% CI" "P-value") ///
	note("Base categories are lowest net worth quartile," \ ///
	"spouse no comorbidities per HRS survey," \ ///
	"and S p1 interview 0-6m after R's death." \ ///
	"Robust standard errors are clustered by HRR.") ///
	starlevels(10 5 1) store(res1) summstat(N) landscape


logit s_cesd_ge3better int_any_of5_6m `rvars2' `svars' `region' if r_hs_3d_6m==0, or vce(cluster hrrnum)
outreg using `logpath'\5_multivar_logit_results, ///
	varlabels stats(e_b e_ci p) nosubstat ///
	ctitles("" "(2) Clinical improvement" "" "" \ "" "OR" "95% CI" "P-value") ///
	starlevels(10 5 1) merge(res1) summstat(N) landscape addtable

regress s_cesdchange int_any_of5_6m `rvars2' `svars' `region' if r_hs_3d_6m==0, vce(cluster hrrnum)
outreg using `logpath'\5_multivar_logit_results, ///
	varlabels stats(e_b e_ci p) nosubstat ///
	title("Outcome = Change in CES-D score, pre to post death" \ ///
	"Excluding observations where R hospice use 3+ days") ///
	ctitles("" "Coefficient" "95% CI" "P-value") ///
		note("Base categories are lowest net worth quartile," \ ///
	"spouse no comorbidities per HRS survey," \ ///
	"and S p1 interview 0-6m after R's death." \ ///
	"CES-D possible range 0-8, Change CES-D range = -8 to +8" \ ///
	"Positive change indicates higher CES-D post death, ie more depressed." \ ///
	"Robust standard errors are clustered by HRR.") ///
	starlevels(10 5 1) summstat(N) landscape addtable


*******************************************************************
log close


H="Set up and run initial propensity score matching"
capture log close

clear all
set more off

local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\6-Spouse_intensive_ps_match.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_cleaned.dta if cesd_miss_n1_p1==0
*******************************************************************

//where r&s highly correlated, use s
local rvars r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 ///
r_nhres_n1n2 r_srh_pf_n1n2 r_advdir_x r_dexp_x  r_adl_impair_n1n2 ///
cc_3_alzhdmta_n12mn6m cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m ///
cc_8_chf_n12mn6m cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m ///
cc_16_strketia_n12mn6m cc_cncr_chronic_n12mn6m cc_15_ra_oa_n12mn6m ///
cc_13_depressn_n12mn0 cc_all_count_6m12m_2

local region hci_index hospital_beds specialists

local svars s_age s_female s_white_e ///
s_hseduc_n1_n3 s_srh_pf_n1n2 s_imprelig_vimp_n1  ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
r_s_help_dur_ind2 r_s_help_dur_ind3 days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr

//drop observations that are missing in any of the variables
foreach v in `rvars' `svars'{
tab `v', missing
drop if `v'==.
}

tab int_any_of5_6m , missing

//save this dataset with no missing variables
save spouse_intens_data_ps_set.dta, replace
*******************************************************************
//look at some additional variables
*******************************************************************
label define decmaker 0 "N/A No EOL care decision made" 1 "Patient" ///
      2 "Spouse or Partner or Exit Int Proxy" ///
      3 "Child/Child-in-law/Grandchild" 4 "Other Relative" 5 "Friend" ///
      6 "Physician/Health Care Provider" 7 "Minister/Rabbi/Priest" ///
      8 "Other" 9 "Don't Know/Refused", modify
label values r_decmaker_x decmaker

tab r_decmaker_x, missing
tab r_eoldec_x, missing
tab r_dexp_x, missing
tab r_capacity_x, missing
tab r_s_help_dur_cat, missing

*******************************************************************
//propensity score generation

//first sort data randomly
set seed 2000
gen x=uniform()
sort x

//generate pscore variable hs_pscore
pscore int_any_of5_6m `rvars' `svars', pscore(ip_pscore) blockid(ip_block) logit detail

psgraph, treated(int_any_of5_6m) pscore(ip_pscore)

*******************************************************************
//implement radius matching
gen logitpscore=ln(ip_pscore/(1-ip_pscore))
sum logitpscore

sca cal_ps1=r(sd)*0.02
sca list cal_ps1

//rule of thumb 0.2*sd, try 0.02* sd
psmatch2  int_any_of5_6m,outcome(s_cesdbetter) pscore(ip_pscore) radius caliper(.0121083) logit
//note this creates varaible _weight

*******************************************************************
//look at balance of propensity score across variables
psgraph, treated(int_any_of5_6m) pscore(ip_pscore)

pstest `rvars' `svars' ,treated(int_any_of5_6m) both hist

//look at quantile-quantile plots for continuous variables
foreach v in r_age cc_all_count_6m12m_2{
gen `v'_treated = `v' if int_any_of5_6m==1 & _support==1
gen `v'_comparison = `v' if int_any_of5_6m==0 & _support==1
qqplot `v'_treated `v'_comparison
graph save `logpath'/qqplot_`v', replace
}

mat vr=J(34,6,.)
//compare variance of covariates before and after matching
local i=1
foreach v in `rvars' `svars'  {
sum `v' if int_any_of5_6m==1 //get variance treated before matching
mat vr[`i',1]=r(Var)
sum `v' if int_any_of5_6m==0 //get variance comparison before matching
mat vr[`i',2]=r(Var)
mat vr[`i',3]= vr[`i',1]/vr[`i',2] //ratio
sum `v' if int_any_of5_6m==1 & _support==1 //get variance treated after matching
mat vr[`i',4]=r(Var)
sum `v' if int_any_of5_6m==0 [iweight=_weight] //get variance comparison after matching
mat vr[`i',5]=r(Var)
mat vr[`i',6]= (vr[`i',4]) / (vr[`i',5]) //ratio
local i=`i'+1
}

mat rownames vr=`rvars' `svars'

mat list vr

//what gets dropped from sample using this match?
tab _support, missing
tab _support if int_any_of5_6m==1, missing //n=1 treated dropped, off comm support
tab _support if int_any_of5_6m==0, missing
tab _weight if int_any_of5_6m==1, missing
sum _weight if int_any_of5_6m==0 //n=2 control dropped, no weight assigned

rename _weight ip_weight

save spouse_intens_data_ps_matched.dta, replace
//use  spouse_intens_data_ps_matched.dta
*******************************************************************
//run the doubly robust models
la var int_any_of5_6m "Intensive procedure used"
la var r_nhres_n1n2 "R Nursing home resident"
la var r_srh_pf_n1n2 "R SRH Fair/Poor"
la var r_adl_impair_n1n2 "R ADL Impaired"
la var cc_all_count_6m12m_2 "R # Chronic conditions"
la var s_hseduc_n1_n3 "S HS Degree"
la var s_srh_pf_n1n2 "S SRH Fair/Poor"
la var s_hrs_comorb_mild_n1 "S Mild comorb."
la var s_hrs_comorb_mod_n1 "S Moderate comorb."
la var s_psych_overall_hrs_n1n2 "S Psych condition"

logit s_cesdbetter int_any_of5_6m `rvars' `svars' [pw=ip_weight], or
outreg, ///
title("Impact of intensive procedure on spousal depression" \ ///
"Propensity matched sample") ///
varlabels stats(e_b e_ci p)  ///
ctitles("","(1) Any improvement" ) ///
starlevels(10 5 1) store(res1) summstat(N)

logit s_cesd_ge3better int_any_of5_6m `rvars' `svars' [pw=ip_weight], or
outreg using `logpath'\6_dr_est_results_ps_matched, replace ///
merge(res1) varlabels stats(e_b e_ci p) ///
ctitles("","(2) Clinical improvement") ///
summstat(N)

regress s_cesdchange int_any_of5_6m `rvars' `svars' [pw=ip_weight]
outreg using `logpath'\6_dr_est_results_ps_matched, ///
merge(res1) replace  varlabels stats(b ci p) ///
ctitles("","(3) Raw change in CES-D score") ///
summstat(N) landscape ///
note("Weighting down control observations, 1 control for each treatment, " \ ///
"for OLS regression on change score results in the reported N") 

*******************************************************************
log close


H="Propensity match - caregivers only"
capture log close

clear all
set more off

local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\6b-Spouse_intense_caregivers.txt", text replace

cd `datapath'

//use dataset with no missing variables, caregivers only
*******************************************************************
use spouse_intens_data_ps_set.dta if (r_adl_sp_helper_x==1 | r_iadl_sp_helper_x==1) 

tab r_s_help_dur_cat, missing
//no one =0, not helper in exit so base for this model is helper exit only

//where r&s highly correlated, use s
local rvars r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 ///
r_nhres_n1n2 r_srh_pf_n1n2 r_advdir_x r_dexp_x  r_adl_impair_n1n2 ///
cc_3_alzhdmta_n12mn6m cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m ///
cc_8_chf_n12mn6m cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m ///
cc_16_strketia_n12mn6m cc_cncr_chronic_n12mn6m cc_15_ra_oa_n12mn6m ///
cc_13_depressn_n12mn0 cc_all_count_6m12m_2

local svars s_age s_female s_white_e ///
s_hseduc_n1_n3 s_srh_pf_n1n2 s_imprelig_vimp_n1  ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
r_s_help_dur_ind3 days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr
*******************************************************************
//propensity score generation

//first sort data randomly
set seed 2000
gen x=uniform()
sort x

//generate pscore variable hs_pscore
pscore int_any_of5_6m `rvars' `svars', pscore(ip_pscore) blockid(ip_block) logit detail

psgraph, treated(int_any_of5_6m) pscore(ip_pscore)

*******************************************************************
//implement radius matching
gen logitpscore=ln(ip_pscore/(1-ip_pscore))
sum logitpscore

sca cal_ps1=r(sd)*0.02
sca list cal_ps1

//rule of thumb 0.2*sd, try 0.02* sd
psmatch2  int_any_of5_6m,outcome(s_cesdbetter) pscore(ip_pscore) radius caliper(.01413846) logit
//note this creates varaible _weight

*******************************************************************
//look at balance of propensity score across variables
psgraph, treated(int_any_of5_6m) pscore(ip_pscore)

pstest `rvars' `svars' ,treated(int_any_of5_6m) both hist

//look at quantile-quantile plots for continuous variables
foreach v in r_age cc_all_count_6m12m_2{
gen `v'_treated = `v' if int_any_of5_6m==1 & _support==1
gen `v'_comparison = `v' if int_any_of5_6m==0 & _support==1
qqplot `v'_treated `v'_comparison
graph save `logpath'/qqplot_`v'_cg, replace
}

mat vr=J(33,6,.)
//compare variance of covariates before and after matching
local i=1
foreach v in `rvars' `svars'  {
sum `v' if int_any_of5_6m==1 //get variance treated before matching
mat vr[`i',1]=r(Var)
sum `v' if int_any_of5_6m==0 //get variance comparison before matching
mat vr[`i',2]=r(Var)
mat vr[`i',3]= vr[`i',1]/vr[`i',2] //ratio
sum `v' if int_any_of5_6m==1 & _support==1 //get variance treated after matching
mat vr[`i',4]=r(Var)
sum `v' if int_any_of5_6m==0 [iweight=_weight] //get variance comparison after matching
mat vr[`i',5]=r(Var)
mat vr[`i',6]= (vr[`i',4]) / (vr[`i',5]) //ratio
local i=`i'+1
}

mat rownames vr=`rvars' `svars'

mat list vr

//what gets dropped from sample using this match?
tab _support, missing
tab _support if int_any_of5_6m==1, missing //n=0 treated dropped, off comm support
tab _support if int_any_of5_6m==0, missing
tab _weight if int_any_of5_6m==1, missing
sum _weight if int_any_of5_6m==0 //n=0 control dropped, no weight assigned

rename _weight ip_weight_cg

*******************************************************************
//run the doubly robust models
la var int_any_of5_6m "Intensive procedure used"
la var r_nhres_n1n2 "R Nursing home resident"
la var r_srh_pf_n1n2 "R SRH Fair/Poor"
la var r_adl_impair_n1n2 "R ADL Impaired"
la var cc_all_count_6m12m_2 "R # Chronic conditions"
la var s_hseduc_n1_n3 "S HS Degree"
la var s_srh_pf_n1n2 "S SRH Fair/Poor"
la var s_hrs_comorb_mild_n1 "S Mild comorb."
la var s_hrs_comorb_mod_n1 "S Moderate comorb."
la var s_psych_overall_hrs_n1n2 "S Psych condition"

logit s_cesdbetter int_any_of5_6m `rvars' `svars' [iw=ip_weight_cg], or
outreg, ///
title("Impact of intensive procedure on spousal depression" \ ///
"Propensity matched sample, Primary cargiver spouses only") ///
varlabels stats(e_b e_ci p)  ///
ctitles("","(1) Any improvement" ) ///
starlevels(10 5 1) store(res1) summstat(N)

logit s_cesd_ge3better int_any_of5_6m `rvars' `svars' [iw=ip_weight_cg], or
outreg using `logpath'\6b_ps_matched_caregivers_only, replace ///
merge(res1) varlabels stats(e_b e_ci p) ///
ctitles("","(2) Clinical improvement") ///
summstat(N)

regress s_cesdchange int_any_of5_6m `rvars' `svars' [iw=ip_weight_cg]
outreg using `logpath'\6b_ps_matched_caregivers_only, ///
merge(res1) replace  varlabels stats(b ci p) ///
ctitles("","(3) Raw change in CES-D score") ///
summstat(N) landscape
*******************************************************************
//quick look at matched sample, outcomes univariate comparison
la var s_cesd_tot_ge3_p1 "S Depressed CESD 3+, p1 core"
la var s_cesd_tot_ge3_p1 "S CES-D Score, p1 core"

mat t2_2m=J(3,4,.)

local r = 1
foreach v in s_cesd_tot_ge3_n1n2 s_cesd_tot_ge3_p1 s_cesdbetter { //indicator variables
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m ==`d' [iweight=ip_weight_cg]
		mat t2_2m[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
	mat t2_2m[`r',3]=(t2_2m[`r',1]/100-t2_2m[`r',2]/100)/sqrt((t2_2m[`r',1]/100* ///
		(1-t2_2m[`r',1]/100)+t2_2m[`r',2]/100*(1-t2_2m[`r',2]/100))/2)
		
	logit int_any_of5_6m `v' [iw=ip_weight_cg]
	test `v'
	mat t2_2m[`r',4] = r(p)
	
	local r = `r'+1
}
mat rownames t2_2m=s_cesd_tot_ge3_n1n2 s_cesd_tot_ge3_p1 s_cesdbetter 
frmttable, statmat(t2_2m) varlabels ///
	sdec(2) store(tab2_2m) 

mat t2_3m=J(3,6,.)
local r = 1
foreach v in s_cesd_tot_n1n2 s_cesd_tot_p1 s_cesdchange  { //continuous variables
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m ==`d' [iweight=ip_weight_cg]
		mat t2_3m[`r',`c']=r(mean)
		mat t2_3m[`r',`c'+1]=r(sd)
		local c = `c'+2	
	}
	mat t2_3m[`r',5]=(t2_3m[`r',1]-t2_3m[`r',3])/sqrt((t2_3m[`r',2]^2+t2_3m[`r',4]^2)/2) 
	
	logit int_any_of5_6m `v' [iw=ip_weight]
	test `v'
	mat t2_3m[`r',6] = r(p)
	
	local r = `r'+1
}
mat rownames t2_3m=s_cesd_tot_n1n2 s_cesd_tot_p1 s_cesdchange 

mat dmat=(0,1,0,1,0,0)
mat ann=(1,1,0,0\1,1,0,0\1,1,0,0)
mat list dmat
mat list ann

mat list t2_3m

frmttable, statmat(t2_3m) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_3m) 

outreg using `logpath'\6b_ps_matched_caregivers_only, ///
replay(tab2_2m) append(tab2_3m) addtable ///
ctitles("" "Intensive procedure" "No procedure" "Std Diff." "p-value") ///
title("Matched sample, S CES-D before and after R's death")

*******************************************************************
log close


H="Manual pscore generation"
/*this is not entirely right, if want to use it, need to look into this more
doesn't drop those off common support
doesn't match, just weights*/

capture log close

clear all
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\6a-Spouse_intensive_manual_ps_match.txt", text replace

cd `datapath'

//use sample with full ces-d information, with missing variables dropped
use spouse_intens_data_ps_set.dta


*******************************************************************
//propensity score generation

//first sort data randomly
set seed 2000
gen x=uniform()
sort x

//where r&s highly correlated, use s
local rvars r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 ///
r_nhres_n1n2 r_srh_pf_n1n2 r_advdir_x r_adl_impair_n1n2 ///
cc_3_alzhdmta_n12mn6m cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m ///
cc_8_chf_n12mn6m cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m ///
cc_16_strketia_n12mn6m cc_cncr_chronic_n12mn6m cc_15_ra_oa_n12mn6m ///
cc_13_depressn_n12mn0 cc_all_count_6m12m_2

local svars s_age s_female s_white_e ///
s_hseduc_n1_n3 s_srh_pf_n1n2 s_imprelig_vimp_n1  ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
 days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr

logit int_any_of5_6m `rvars' `svars'

predict predic_prob, pr

gen w=predic_prob/(1-predic_prob)

logit s_cesdbetter int_any_of5_6m `rvars' `svars' [pw=w], or
regress s_cesdchange int_any_of5_6m `rvars' `svars' [pw=w]


*******************************************************************
log close


H="xxxPlot cesd vs time"
/*creates two versions of the plots, first has 6m time windows
second is 1 year time windows to get smaller 95%CI's with larger groups*/

capture log close

clear all
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\7-Spouse_intensive_cesd_vs_time.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_cleaned.dta if cesd_miss_n1_p1==0
*******************************************************************
//create pre death time variables

//first set of time variables - 6m time windows
//use n1 ivw if >6m pre death, n2 otherwise
gen s_days_n_core_dead = s_days_n1_core_dead if s_ind_n1_wi_6m_dead==0
replace s_days_n_core_dead = s_days_n2_core_dead if s_ind_n1_wi_6m_dead==1

gen days_n_core_dod_cat = .
replace days_n_core_dod_cat = 0 if 0<=s_days_n_core_dead & s_days_n_core_dead <183
replace days_n_core_dod_cat = 1 if 183<=s_days_n_core_dead & s_days_n_core_dead <366
replace days_n_core_dod_cat = 2 if 366<=s_days_n_core_dead & s_days_n_core_dead <548
replace days_n_core_dod_cat = 3 if 548<=s_days_n_core_dead & s_days_n_core_dead <731
replace days_n_core_dod_cat = 4 if 731<=s_days_n_core_dead & s_days_n_core_dead !=.
la def days_n_core_dod_cat 0 "0-6 months" 1 "6-12 months" 2 "12-18 months" ///
3 "18-24 months" 4 "24+ months"
la var days_n_core_dod_cat "Days from pre-death core ivw to  R death, categorical"
la val days_n_core_dod_cat days_n_core_dod_cat 
tab days_n_core_dod_cat , missing

tab days_p1_core_dod_cat, missing 

//second set of time variables - 12m time windows
gen n_core_cat2=0 if inlist(days_n_core_dod_cat,0,1)
replace n_core_cat2=1 if inlist(days_n_core_dod_cat,2,3)
replace n_core_cat2=2 if days_n_core_dod_cat==4
la def n_core_cat2 0 "0-12 months" 1"12-24 months" 2"24+ months"
la val n_core_cat2 n_core_cat2

gen p_core_cat2=0 if inlist(days_p1_core_dod_cat,0,1)
replace p_core_cat2=1 if inlist(days_p1_core_dod_cat,2,3)
replace p_core_cat2=2 if days_p1_core_dod_cat==4
la def p_core_cat2 0 "0-12 months" 1"12-24 months" 2"24+ months"
la val p_core_cat2 p_core_cat2

tab n_core_cat2 days_n_core_dod_cat, missing
tab p_core_cat2 days_p1_core_dod_cat, missing

****************************************************************
//Plot using 6m time windows
****************************************************************
mat mean_pro0=J(10,1,.) //means if procedure=no
mat mean_pro1=J(10,1,.) //means if procedure=yes
mat v_pro0=J(10,1,.)
mat v_pro1=J(10,1,.)

//get pre death cesd scores, by intensive procedures and time
mat struct_n=[4,1 \ 3,2 \ 2,3 \ 1,4 ]
mat list struct_n

forvalues r=1/4{
mean s_cesd_tot_n1n2 if days_n_core_dod_cat==struct_n[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_pro0[struct_n[`r',2],1]= mean[1,1]
mat mean_pro1[struct_n[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_pro0[struct_n[`r',2],1]= v[1,1]
mat v_pro1[struct_n[`r',2],1]= v[2,2]
}

//post death cesd, by intensive procedures and time
mat struct_p=[0,6 \ 1,7 \ 2,8 \ 3,9 \ 4,10]
mat list struct_p

forvalues r=1/5{
mean s_cesd_tot_p1 if days_p1_core_dod_cat==struct_p[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_pro0[struct_p[`r',2],1]= mean[1,1]
mat mean_pro1[struct_p[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_pro0[struct_p[`r',2],1]= v[1,1]
mat v_pro1[struct_p[`r',2],1]= v[2,2]
}

svmat mean_pro0
svmat mean_pro1
svmat v_pro0
svmat v_pro1

gen int sitenum = 1 in 1/10
forvalues i=2/10{
replace sitenum=`i' in `i'
}

list sitenum mean_pro0 mean_pro1 v_pro0 v_pro1 in 1/10

foreach x in 0 1{
gen se_pro`x'=sqrt(v_pro`x')

gen ll_pro`x'=mean_pro`x'-1.96*se_pro`x'
gen ul_pro`x'=mean_pro`x'+1.96*se_pro`x'
}

list sitenum mean_pro0 se_pro0 ll_pro0 ul_pro0 mean_pro1 se_pro1 ll_pro1 ul_pro1 in 1/10


twoway rcap ll_pro0 ul_pro0 sitenum, color(gs9) ///
	|| scatter mean_pro0 sitenum, color(midblue) ///
	|| rcap ll_pro1 ul_pro1 sitenum, color(gs5) ///
	|| scatter mean_pro1 sitenum, color(orange) ///
	xline(5) ytitle("Mean CES-D score") ///
	xlabel(1 "24+" 2 "18-24" 3 "12-18" 4"6-12" 5"0" 6"0-6" 7"6-12" ///
	8"12-18" 9"18-24" 10"24+") xtitle("Months relative to R's death") ///
	legend(order(4 3 2 1) symxsize(*.5) cols(2) ///
	label(1 95% confidence interval) label(2 No Itensive Procedure) ///
		label(3 95% confidence interval) label(4 Intensive Procedure) )  ///
	title("Mean CES-D score by time of interview" ///
	"and intensive procedure utilization")
graph save `logpath'/mean_cesd_time,replace
graph export `logpath'/mean_cesd_time.eps,replace

drop mean_pro0 mean_pro1 v_pro0 v_pro1 sitenum se_pro0 se_pro1 ///
ll_pro0 ll_pro1 ul_pro0 ul_pro1
****************************************************************
//Plot using 12m time windows
****************************************************************
mat mean_pro0=J(7,1,.) //means if procedure=no
mat mean_pro1=J(7,1,.) //means if procedure=yes
mat v_pro0=J(7,1,.)
mat v_pro1=J(7,1,.)

//get pre death cesd scores, by intensive procedures and time
mat struct_n=[0,1 \ 1,2 \ 2,3 ]
mat list struct_n

forvalues r=1/3{
mean s_cesd_tot_n1n2 if  n_core_cat2==struct_n[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_pro0[struct_n[`r',2],1]= mean[1,1]
mat mean_pro1[struct_n[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_pro0[struct_n[`r',2],1]= v[1,1]
mat v_pro1[struct_n[`r',2],1]= v[2,2]
}

//post death cesd, by intensive procedures and time
mat struct_p=[0,5 \ 1,6 \ 2,7 ]
mat list struct_p

forvalues r=1/3{
mean s_cesd_tot_p1 if  p_core_cat2==struct_p[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_pro0[struct_p[`r',2],1]= mean[1,1]
mat mean_pro1[struct_p[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_pro0[struct_p[`r',2],1]= v[1,1]
mat v_pro1[struct_p[`r',2],1]= v[2,2]
}

svmat mean_pro0
svmat mean_pro1
svmat v_pro0
svmat v_pro1

gen int sitenum = 1 in 1/7
forvalues i=2/7{
replace sitenum=`i' in `i'
}

list sitenum mean_pro0 mean_pro1 v_pro0 v_pro1 in 1/7

foreach x in 0 1{
gen se_pro`x'=sqrt(v_pro`x')

gen ll_pro`x'=mean_pro`x'-1.96*se_pro`x'
gen ul_pro`x'=mean_pro`x'+1.96*se_pro`x'
}

list sitenum mean_pro0 se_pro0 ll_pro0 ul_pro0 mean_pro1 se_pro1 ll_pro1 ul_pro1 in 1/7


twoway rcap ll_pro0 ul_pro0 sitenum, color(gs9) ///
	|| scatter mean_pro0 sitenum, color(midblue) ///
	|| rcap ll_pro1 ul_pro1 sitenum, color(gs5) ///
	|| scatter mean_pro1 sitenum, color(orange) ///
	xline(4) ytitle("Mean CES-D score") ///
	xlabel(1 "24+" 2 "12-24" 3 "0-12" 4"0" 5"0-12" 6"12-24" 7"24+") ///
	xtitle("Months relative to R's death") ///
	legend(order(4 3 2 1) symxsize(*.5) cols(2) ///
	label(1 95% confidence interval) label(2 No Itensive Procedure) ///
		label(3 95% confidence interval) label(4 Intensive Procedure) )  ///
	title("Mean CES-D score by time of interview" ///
	"and intensive procedure utilization")
graph save `logpath'/mean_cesd_time_12m,replace
graph export `logpath'/mean_cesd_time_12m.eps,replace
*******************************************************************
log close


H="xxxPlot Percent Improvement CESD vs time ivw"
/*creates two versions of the plots, first has 6m time windows
second is 0-12m / 12m+ to get smaller 95%CI's with larger groups*/

capture log close

clear all
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\8-Spouse_intensive_pct_impr_vs_time.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_cleaned.dta if cesd_miss_n1_p1==0
*****************************************************************
mean s_cesdbetter, over(int_any_of5_6m)
mean s_cesd_ge3better, over(int_any_of5_6m)

tab days_p1_core_dod_cat, missing 

mat mean_pro0=J(5,1,.) //means if procedure=no
mat mean_pro1=J(5,1,.) //means if procedure=yes
mat v_pro0=J(5,1,.)
mat v_pro1=J(5,1,.)

mat struct_p=[0,1 \ 1,2 \ 2,3 \ 3,4 \ 4,5]
mat list struct_p

forvalues r=1/5{
mean s_cesdbetter if days_p1_core_dod_cat==struct_p[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_pro0[struct_p[`r',2],1]= mean[1,1]
mat mean_pro1[struct_p[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_pro0[struct_p[`r',2],1]= v[1,1]
mat v_pro1[struct_p[`r',2],1]= v[2,2]
}

svmat mean_pro0
svmat mean_pro1
svmat v_pro0
svmat v_pro1

gen int sitenum = 1 in 1/5
forvalues i=2/5{
replace sitenum=`i' in `i'
}

list sitenum mean_pro0 mean_pro1 v_pro0 v_pro1 in 1/5

foreach x in 0 1{
gen se_pro`x'=sqrt(v_pro`x')

gen ll_pro`x'=mean_pro`x'-1.96*se_pro`x'
gen ul_pro`x'=mean_pro`x'+1.96*se_pro`x'
}

list sitenum mean_pro0 se_pro0 ll_pro0 ul_pro0 mean_pro1 se_pro1 ll_pro1 ul_pro1 in 1/5


twoway rcap ll_pro0 ul_pro0 sitenum, color(gs9) ///
	|| scatter mean_pro0 sitenum, color(midblue) ///
	|| rcap ll_pro1 ul_pro1 sitenum, color(gs5) ///
	|| scatter mean_pro1 sitenum, color(orange) ///
	ytitle("% Spouses with Improved CES-D score") ///
	xlabel(1 "0-6" 2 "6-12" 3 "12-18" 4"18-24" 5"24+") ///
	xtitle("Months after to R's death") ///
	legend(order(4 3 2 1) symxsize(*.5) cols(2) ///
	label(1 95% confidence interval) label(2 No Itensive Procedure) ///
		label(3 95% confidence interval) label(4 Intensive Procedure) )  ///
	title("Any Improvement in CES-D score by time of interview" ///
	"and intensive procedure utilization")
graph save `logpath'/imp_cesd_time,replace
graph export `logpath'/imp_cesd_time.eps,replace

drop mean_pro0 mean_pro1 v_pro0 v_pro1 sitenum se_pro0 se_pro1 ///
ll_pro0 ll_pro1 ul_pro0 ul_pro1

*****************************************************************
//collapsed to before / after 1 year time periods
//adding % clinical improvement
tab days_p1_core_dod_cat, missing 
gen p_core_cat2=0 if inlist(days_p1_core_dod_cat,0,1)
replace p_core_cat2=1 if inlist(days_p1_core_dod_cat,2,3,4)
la def p_core_cat2 0 "0-12 months" 1"12+ months"
la val p_core_cat2 p_core_cat2

mat mean_pro0=J(2,1,.) //means if procedure=no
mat mean_pro1=J(2,1,.) //means if procedure=yes
mat v_pro0=J(2,1,.)
mat v_pro1=J(2,1,.)

mat mean_cl_pro0=J(2,1,.) //means if procedure=no for clinical improvement
mat mean_cl_pro1=J(2,1,.) //means if procedure=yes
mat v_cl_pro0=J(3,1,.)
mat v_cl_pro1=J(3,1,.)

mat struct_p=[0,1 \ 1,2 ]
mat list struct_p

forvalues r=1/2{
mean s_cesdbetter if p_core_cat2==struct_p[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_pro0[struct_p[`r',2],1]= mean[1,1]
mat mean_pro1[struct_p[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_pro0[struct_p[`r',2],1]= v[1,1]
mat v_pro1[struct_p[`r',2],1]= v[2,2]

mean s_cesd_ge3better if p_core_cat2==struct_p[`r',1], over(int_any_of5_6m)
mat mean=e(b)
mat mean_cl_pro0[struct_p[`r',2],1]= mean[1,1]
mat mean_cl_pro1[struct_p[`r',2],1]= mean[1,2]
mat v=e(V)
mat v_cl_pro0[struct_p[`r',2],1]= v[1,1]
mat v_cl_pro1[struct_p[`r',2],1]= v[2,2]

}

svmat mean_pro0
svmat mean_pro1
svmat v_pro0
svmat v_pro1
svmat mean_cl_pro0
svmat mean_cl_pro1
svmat v_cl_pro0
svmat v_cl_pro1

gen int sitenum = 1 in 1/2
replace sitenum=2 in 2

gen float sitenum2=0.8 in 1/2
replace sitenum2=1.8 in 2

list sitenum mean_pro0 mean_pro1 v_pro0 v_pro1 in 1/2

list sitenum2 mean_cl_pro0 mean_cl_pro1 v_cl_pro0 v_cl_pro1 in 1/2

foreach x in 0 1{
gen se_pro`x'=sqrt(v_pro`x')

gen ll_pro`x'=mean_pro`x'-1.96*se_pro`x'
gen ul_pro`x'=mean_pro`x'+1.96*se_pro`x'

gen se_cl_pro`x'=sqrt(v_cl_pro`x')

gen ll_cl_pro`x'=mean_cl_pro`x'-1.96*se_cl_pro`x'
gen ul_cl_pro`x'=mean_cl_pro`x'+1.96*se_cl_pro`x'
}

list sitenum mean_pro0 se_pro0 ll_pro0 ul_pro0 mean_pro1 se_pro1 ll_pro1 ul_pro1 in 1/2
list sitenum2 mean_cl_pro0 se_cl_pro0 ll_cl_pro0 ul_cl_pro0 mean_cl_pro1 ///
	se_cl_pro1 ll_cl_pro1 ul_cl_pro1 in 1/2


twoway rcap ll_cl_pro0 ul_cl_pro0 sitenum2, color(gs9) /// //clin impr w/o 
	|| scatter mean_cl_pro0 sitenum2, color(navy) ///
	|| rcap ll_pro0 ul_pro0 sitenum, color(gs9) ///           //any impr w/o
	|| scatter mean_pro0 sitenum, color(midblue) ///
	|| rcap ll_cl_pro1 ul_cl_pro1 sitenum2, color(gs5) /// //clin impr w 
	|| scatter mean_cl_pro1 sitenum2, color(dkorange) ///
	|| rcap ll_pro1 ul_pro1 sitenum, color(gs5) /// //any impr w
	|| scatter mean_pro1 sitenum, color(orange) ///
	ytitle("% Spouses with CES-D improvement") ///
	xlabel(1 "0-12" 2 "12+") ///
	xtitle("Months after to R's death") ///
	legend(order(8 6 4 2) symxsize(*.5) cols(2) ///
	label(2 No Itensive Procedure, Clinical Impr.) ///
	label(4 No Intensive Procedure, Any Impr.) ///
	label(6 Intensive procedure, Clinical Impr.) ///
	label(8 Intensive procedure, Any Impr.))  ///
	title("Improvement in CES-D score by time of interview" ///
	"and intensive procedure utilization")
graph save `logpath'/imp_cesd_time_12m,replace
graph export `logpath'/imp_cesd_time_12m.eps,replace
*****************************************************************
log close


H="Bar graph cesd vs time"
//use dataset where observations missing data for psmatched variables are dropped
capture log close

clear all
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\9-Spouse_intensive_bar_chart.txt", text replace

cd `datapath'

//use sample with full ces-d information
use spouse_intens_data_ps_matched.dta
***************************************************************
//set time variables
gen s_days_n_core_dead = s_days_n1_core_dead if s_ind_n1_wi_6m_dead==0
replace s_days_n_core_dead = s_days_n2_core_dead if s_ind_n1_wi_6m_dead==1

gen days_n_core_dod_cat = .
replace days_n_core_dod_cat = 0 if 0<=s_days_n_core_dead & s_days_n_core_dead <183
replace days_n_core_dod_cat = 1 if 183<=s_days_n_core_dead & s_days_n_core_dead <366
replace days_n_core_dod_cat = 2 if 366<=s_days_n_core_dead & s_days_n_core_dead <548
replace days_n_core_dod_cat = 3 if 548<=s_days_n_core_dead & s_days_n_core_dead <731
replace days_n_core_dod_cat = 4 if 731<=s_days_n_core_dead & s_days_n_core_dead !=.
la def days_n_core_dod_cat 0 "0-6 months" 1 "6-12 months" 2 "12-18 months" ///
3 "18-24 months" 4 "24+ months"
la var days_n_core_dod_cat "Days from pre-death core ivw to  R death, categorical"
la val days_n_core_dod_cat days_n_core_dod_cat 
tab days_n_core_dod_cat , missing

tab days_p1_core_dod_cat, missing 

//now create cesd variables for each of the time periods,
//leave missing if cesd not taking within the time window
gen cesd_1=s_cesd_tot_n1n2 if days_n_core_dod_cat==4 //n1 core 24+m
gen cesd_2=s_cesd_tot_n1n2 if days_n_core_dod_cat==3 //n1 core 18-24m
gen cesd_3=s_cesd_tot_n1n2 if days_n_core_dod_cat==2
gen cesd_4=s_cesd_tot_n1n2 if days_n_core_dod_cat==1
gen cesd_5=s_cesd_tot_n1n2 if days_n_core_dod_cat==0
gen cesd_6=s_cesd_tot_p1 if days_p1_core_dod_cat==0
gen cesd_7=s_cesd_tot_p1 if days_p1_core_dod_cat==1
gen cesd_8=s_cesd_tot_p1 if days_p1_core_dod_cat==2
gen cesd_9=s_cesd_tot_p1 if days_p1_core_dod_cat==3
gen cesd_10=s_cesd_tot_p1 if days_p1_core_dod_cat==4

//drop variables don't need
keep r_id cesd_* int_any_of5_6m ip_weight
drop cesd_miss*

//restructure dataset, 2 rows per person, one before death, one after death
reshape long cesd_, i(r_id) j(time)

save cesd_bar_graph.dta, replace
*****************************************************************
//do pre-match graph
collapse cesd_, by(time int_any_of5_6m)

//again restructure so have one obs per time, but two mean cesd
preserve
keep if int_any_of5_6m==1
rename cesd_ cesd_int
save int_1.dta, replace
restore

keep if int_any_of5_6m==0

merge 1:1 time using int_1.dta

graph bar cesd_ cesd_int,  over(time, relabel(1 "24+" 2 "18-24m" 3 "12-18" ///
	4 "6-12" 5 "R's death" 6 "0-6" 7"6-12" 8"12-18" 9"18-24" 10"24+" )) ///
	legend(label(1 No intensive procedure) label(2 Intensive procedure)) ///
	ytitle("Mean CES-D score") ///
	title("Spouse Mean CES-D score by time of interview, not matched") ///
	subtitle("Months before & after R's death")
	
graph save `logpath'/mean_cesd_bar_6m_not_matched, replace

*****************************************************************
//now post-match graph
use cesd_bar_graph.dta, clear

collapse cesd_ [pw=ip_weight], by(time int_any_of5_6m)

preserve //again restructure so have one obs per time, but two mean cesd
keep if int_any_of5_6m==1
rename cesd_ cesd_int
save int_1.dta, replace
restore

keep if int_any_of5_6m==0

merge 1:1 time using int_1.dta

graph bar cesd_ cesd_int,  over(time, relabel(1 "24+" 2 "18-24m" 3 "12-18" ///
	4 "6-12" 5 "R's death" 6 "0-6" 7"6-12" 8"12-18" 9"18-24" 10"24+" )) ///
	legend(label(1 No intensive procedure) label(2 Intensive procedure)) ///
	ytitle("Mean CES-D score") ///
	title("Spouse Mean CES-D score by time of interview, matched") ///
	subtitle("Months before & after R's death")
	
graph save `logpath'/mean_cesd_bar_6m_matched, replace
***************************************************************

//a simple figure, this time mean before vs after by intense procedure groups
use spouse_intens_data_ps_matched.dta, clear

collapse s_cesd_tot_n1n2 s_cesd_tot_p1 [pw=ip_weight], by(int_any_of5_6m)

rename s_cesd_tot_n1n2 cesd1
rename s_cesd_tot_p1 cesd2

reshape long cesd, i(int_any_of5_6m) j(time)

la var int_any_of5_6m "Intensive procedure last 6m of life"
la def int_any_of5_6m 0 "No intensive procedure" 1"Intensive procedure"
la val int_any_of5_6m int_any_of5_6m

scatter cesd time, by(int_any_of5_6m) connect(direct) ///
ytitle("Mean CES-D Score") ///
xtitle("Interview relative to R's death") ///
xlabel(1 "Before" 2 "After") ///
xscale(range(.75 2.25))

graph save `logpath'/mean_cesd_by_proced_matched, replace

***************************************************************
log close


H="Create Tables 1 and 2"
/*creates 3 tables:
Table 1: Overall sample characteristics, not matched
Table 2: Not matched / matched char by intensive procedure use, shows std differences
to demonstrate propensity score matched sample is well matched on all included covariates
Table 3: CES-D score before/after by intensive procedure use, matched and not matched
*/

capture log close

clear all
set more off

//Amy's PC
local logpath E:\data\spouse_intensive\logs
local datapath E:\data\spouse_intensive\data

log using "`logpath'\10-Spouse_intensive_tables12.txt", text replace

cd `datapath'

//use sample with no missingness - saved when first round of psmatching done
use spouse_intens_data_ps_matched.dta
***************************************************************
tab int_any_of5_6m, missing

*************************************************************************************
*************************************************************************************
//create Table 1, overall sample characteristics, prior to matching
*************************************************************************************
*************************************************************************************

//Part 1 - Decedent char
local rvars r_female r_white_e r_hseduc_n1_n3 r_nw_lowest_n1 ///
r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 r_medicaid_n1 r_champus_n1 r_medigap_n1 ///
r_nhres_n1n2 r_advdir_x r_dexp_x  r_discuss_x ///
r_adl_impair_n1n2 r_srh_pf_n1n2 int_any_of5_6m ///
cc_3_alzhdmta_n12mn6m ///
cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m cc_8_chf_n12mn6m ///
cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m cc_16_strketia_n12mn6m ///
cc_cncr_chronic_n12mn6m cc_4_atrialfb_n12mn6m cc_11_hipfrac_n12mn6m ///
cc_13_depressn_n12mn6m cc_14_osteoprs_n12mn6m cc_15_ra_oa_n12mn6m 

//r cont vars
//r_age cc_all_count_6m12m_2 
 
la var r_age "Age at death, years, mean (SD)"
la var r_female "Female, %"
la var r_white_e "Race, Non-Hispanic, White, %"
la var r_hseduc_n1_n3 "Education, High School Grad, %"
la var r_nw_lowest_n1 "Net Worth, 1 (low) Quartile, %"
la var r_nw_midlow_n1  "Net Worth, 2 Quartile, %"
la var r_nw_midhigh_n1  "Net Worth, 3 Quartile, %"
la var r_nw_highest_n1  "Net Worth, 4 (high) Quartile, %"
la var r_medicaid_n1  "Medicaid, %"
la var r_champus_n1  "VA Insurance, %"
la var r_medigap_n1 "Medigap, %"
la var r_nhres_n1n2 "Nursing home resident pre-death, %**"
la var r_advdir_x "Had advanced directive, %"
la var r_dexp_x  "Death expected by family, %"
la var r_discuss_x "Discussion of EOL care, %"
la var r_adl_impair_n1n2 "ADL Impairment, %**"
la var r_srh_pf_n1n2 "SRH poor or fair, %**"
la var cc_all_count_6m12m_2 "Number of chronic conditions, mean (SD)"

mat rchar_1=J(1,2,.)

sum r_age, detail //age
mat rchar_1[1,1]=r(mean)
mat rchar_1[1,2]=r(sd)

sum s_age, detail //get overall sample n
local samplen=r(N)

mat rownames rchar_1=r_age

//save the table in frmttable	
mat dmat=(0,1)
mat ann=J(1,1,1)

frmttable, statmat(rchar_1) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1_1) 	

mat rchar_2=J(30,1,.)
local r = 1
foreach v in `rvars'{
	sum `v'
	mat rchar_2[`r',1]=r(mean)*100 //percent
	local r = `r'+1
	}

mat rownames rchar_2 = `rvars'

frmttable, statmat(rchar_2) varlabels ///
	sdec(2) store(tab1_2) 	  

mat rchar_3=J(1,2,.)

sum cc_all_count_6m12m_2, detail //cc's count
mat rchar_3[1,1]=r(mean)
mat rchar_3[1,2]=r(sd)

mat rownames rchar_3=cc_all_count_12m_2

//save the table in frmttable	
mat dmat=(0,1)
mat ann=J(1,1,1)

frmttable, statmat(rchar_3) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1_3) 	  

outreg, replay(tab1_1) append(tab1_2) store(tab1_a)

*** add the headings, notes, using file name etc. to this one
outreg using "`logpath'\10_spo_ip_tab1and2", ///
replay(tab1_a) append(tab1_3) ///
title("Table 1: Characteristics of Study sample (n=`samplen')") ///
ctitles("Decedent","") ///
note("Decedent chronic conditions from Medicare claims. 6-12m before death." \ ///
"Spouse chronic conditions from self report in HRS interviews." \ ///
"** From the pre-death interview at least 6m prior to death.") ///
landscape replace

**************************************************
//Part 2 - Spouse char

//just check no severe category
tab s_hrs_comorb_mod_n1 s_comor_c_hrs_n1, missing

local svars s_female s_white_e s_hseduc_n1_n3 ///
s_medicaid_n1 s_adl_independent_core_n1n2 s_srh_pf_n1n2 s_imprelig_vimp_n1 ///
s_cancer_hrs_n1 s_lung_hrs_n1 s_heart_hrs_n1 s_chf_hrs_n1 ///
s_stroke_hrs_n1 s_memory_hrs_n1 s_htn_hrs_n1 s_dm_hrs_n1 ///
s_hrs_comorb_none_n1 s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 ///
s_psych_overall_hrs_n1n2 s_smoke_curr_n1 s_vig_phy_act_n1 ///
r_s_help_dur_ind1 r_s_help_dur_ind2 r_s_help_dur_ind3 ///
s_cesd_tot_ge3_n1n2 s_cesd_tot_ge3_p1 s_cesdbetter s_cesd_ge3better 

la var s_age "Age at spouse's death, years, mean (SD)"
la var s_female "Female, %"
la var s_white_e "Race, Non-Hispanic, White, %"
la var s_hseduc_n1_n3 "Education, High School Grad, %"
la var s_medicaid_n1  "Medicaid, %"
la var s_adl_independent_core_n1n2 "ADL Independent pre-death, %**"
la var s_srh_pf_n1n2 "SRH poor or fair, %**"
la var s_imprelig_vimp_n1 "Religion very important, %"
la var s_cancer_hrs_n1 "Cancer, %"
la var s_lung_hrs_n1 "Lung disease, %"
la var s_heart_hrs_n1 "Heart disease, %"
la var s_chf_hrs_n1 "CHF, %"
la var s_stroke_hrs_n1 "Stroke, %"
la var s_memory_hrs_n1 "Memory disease, %"
la var s_htn_hrs_n1 "Hypertension, %"
la var s_dm_hrs_n1 "Diabetes, %"
la var s_hrs_comorb_none_n1 "Comorbidities, none, %"
la var s_hrs_comorb_mild_n1 "Comorbidities, Mild (1-3), %"
la var s_hrs_comorb_mod_n1 "Comorbidities, Moderate (4-6), %"
la var s_psych_overall_hrs_n1n2 "Psychological problems, %**"
la var s_smoke_curr_n1 "Currently smoker, %"
la var s_vig_phy_act_n1 "Vigorous physical activity, %"
la var r_s_help_dur_ind1 "Spouse not primary caregiver exit, %"
la var r_s_help_dur_ind2 "Spouse primary caregiver exit only, %"
la var r_s_help_dur_ind3 "Spouse primary caregiver exit + core, %"
la var s_cesd_tot_ge3_n1n2 "S Depressed CESD 3+, pre-death, %**"
la var s_cesd_tot_ge3_p1 "S Depressed CESD 3+, post-death, %"
la var s_cesdbetter "S CES-D Improved n1 to p1,%"
la var s_cesd_ge3better "Clinical improvement n1 to p1,%"

la var s_cesd_tot_n1n2 "CESD score pre-death, mean(SD)**"
la var s_cesd_tot_p1 "CESD score post-death, mean(SD)"
la var s_cesdchange "Change CES-D Score n1 to p1, mean(SD)"

mat schar_1=J(1,2,.)

sum s_age, detail //age
mat schar_1[1,1]=r(mean)
mat schar_1[1,2]=r(sd)

mat rownames schar_1=s_age

//save the table in frmttable	
mat dmat=(0,1)
mat ann=J(1,1,1)

frmttable, statmat(schar_1) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1_1) 	

mat schar_2=J(28,1,.)
local r = 1
foreach v in `svars'{
	sum `v'
	mat schar_2[`r',1]=r(mean)*100 //percent
	local r = `r'+1
	}

mat rownames schar_2 = `svars'

frmttable, statmat(schar_2) varlabels ///
	sdec(2) store(tab1_2) 	  

mat schar_3=J(3,2,.)

local r = 1
foreach v in s_cesd_tot_n1n2 s_cesd_tot_p1 s_cesdchange {
	sum `v', detail //ces-d scores
	mat schar_3[`r',1]=r(mean)
	mat schar_3[`r',2]=r(sd)
	local r = `r'+1
	}

mat rownames schar_3=s_cesd_tot_n1n2 s_cesd_tot_p1 s_cesdchange

//save the table in frmttable	
mat dmat=(0,1)
mat ann=J(3,1,1)

frmttable, statmat(schar_3) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab1_3) 	  

outreg, replay(tab1_1) append(tab1_2) store(tab1_a)

*** add the headings, notes, using file name etc. to this one
outreg using "`logpath'\10_spo_ip_tab1and2", ///
replay(tab1_a) append(tab1_3) ///
title("Table 1: Characteristics of Study sample (n=`samplen')") ///
ctitles("Spouse","") ///
note("Decedent chronic conditions from Medicare claims. 6-12m before death." \ ///
"Spouse chronic conditions from self report in HRS interviews." \ ///
"** From the pre-death interview at least 6m prior to death.") ///
landscape addtable


*************************************************************************************
*************************************************************************************
//Create Table 2, Decedent and Spouse PS Score Matching Variables
//Col 1-3: Unweighted, unmatched sample
//Col 4-6: Weighted and matched sample
*************************************************************************************
*************************************************************************************

local t2vars1 r_nw_midlow_n1 r_nw_midhigh_n1 r_nw_highest_n1 ///
r_nhres_n1n2 r_srh_pf_n1n2 r_advdir_x r_dexp_x r_adl_impair_n1n2 ///
cc_3_alzhdmta_n12mn6m cc_6_chrnkidn_n12mn6m cc_ami_isch_n12mn6m ///
cc_8_chf_n12mn6m cc_9_diabetes_n12mn6m cc_7_copd_n12mn6m ///
cc_16_strketia_n12mn6m cc_cncr_chronic_n12mn6m cc_15_ra_oa_n12mn6m ///
cc_13_depressn_n12mn0 

local t2vars2 cc_all_count_6m12m_2 s_age 

local t2vars3 s_female s_white_e ///
s_hseduc_n1_n3 s_srh_pf_n1n2 s_imprelig_vimp_n1  ///
s_hrs_comorb_mild_n1 s_hrs_comorb_mod_n1 s_psych_overall_hrs_n1n2 ///
r_s_help_dur_ind2 r_s_help_dur_ind3 days_p1_core_dod_6_12m days_p1_core_dod_12_18m ///
days_p1_core_dod_18_24m days_p1_core_dod_gt2yr

la var r_nhres_n1n2 "Nursing home resident pre-death"
la var r_srh_pf_n1n2 "SRH poor or fair"
la var r_adl_impair_n1n2 "ADL Impairment"
la var s_srh_pf_n1n2 "SRH poor or fair"
la var s_psych_overall_hrs_n1n2 "Psychological problems"
	
//part 1, n unmatched
mat t2_1=J(1,3,.)
tab int_any_of5_6m , missing matcell(d1)
mat t2_1[1,2]=d1[1,1] //no procedures n
mat t2_1[1,1]=d1[2,1] //procedure n
mat rownames t2_1="Sample n"

frmttable, statmat(t2_1) varlabels ///
	sdec(0) store(tab2_1) 	

//part 2, decedent char, % and std difference
mat t2_2=J(18,5,.)

local r = 1
foreach v in `t2vars1'{
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m==`d', detail
		mat t2_2[`r',`c']=r(mean)*r(N) //n
		mat t2_2[`r',`c'+1]=r(mean)*100 //percent
		local c = `c'+2		
	}
	mat t2_2[`r',5]=(t2_2[`r',2]/100-t2_2[`r',4]/100)/sqrt((t2_2[`r',2]/100* ///
		(1-t2_2[`r',2]/100)+t2_2[`r',4]/100*(1-t2_2[`r',4]/100))/2)
	local r = `r'+1
}
mat rownames t2_2=`t2vars1'

mat dmat=(0,1,0,1,0)
mat ann=J(17,2,1)

frmttable, statmat(t2_2) varlabels ///
	sdec(1,1,2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_2) 	

//part 3, cont vars r & s, mean and std diff
mat t2_3=J(2,5,.)

local r = 1
foreach v in `t2vars2'{
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m==`d', detail
		mat t2_3[`r',`c']=r(mean)
		mat t2_3[`r',`c'+1]=r(sd)
		local c = `c'+2		
	}
	mat t2_3[`r',5]=(t2_3[`r',1]-t2_3[`r',3])/sqrt((t2_3[`r',2]^2+t2_3[`r',4]^2)/2) 
	local r = `r'+1
}

mat rownames t2_3=`t2vars2'

mat dmat=(0,1,0,1,0)
mat ann=(1,1,0\1,1,0)
mat list dmat
mat list ann

mat list t2_3

frmttable, statmat(t2_3) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_3) 

//part 4, spouse char, %
mat t2_4=J(14,5,.)

local r = 1
foreach v in `t2vars3'{
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m==`d', detail
		mat t2_4[`r',`c']=r(mean)*r(N)
		mat t2_4[`r',`c'+1]=r(mean)*100
		local c = `c'+2		
	}
	mat t2_4[`r',5]=(t2_4[`r',2]/100-t2_4[`r',4]/100)/sqrt((t2_4[`r',2]/100* ///
		(1-t2_4[`r',2]/100)+t2_4[`r',4]/100*(1-t2_4[`r',4]/100))/2)

	local r = `r'+1
}
mat rownames t2_4=`t2vars3'

mat dmat=(0,1,0,1,0)
mat ann=J(12,2,1)

frmttable, statmat(t2_4) varlabels ///
	sdec(1,1,2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_4) 	

outreg, replay(tab2_1) append(tab2_2) store(tab2_a)
outreg, replay(tab2_a) append(tab2_3) store(tab2_b)
outreg, replay(tab2_b) append(tab2_4) store(tab2_c) //complete unmatched table w/ std diff

***************************************************************
//end of unmatched sample, start matched sample
***************************************************************
//part 1, n matched
mat t2_1m=J(1,3,.)
tab int_any_of5_6m if ip_weight!=., missing matcell(d1)
mat t2_1m[1,2]=d1[1,1] //no procedures n
mat t2_1m[1,1]=d1[2,1] //procedure n
mat rownames t2_1m="Sample n"

frmttable, statmat(t2_1m) varlabels ///
	sdec(0) store(tab2_1m) 	

//part 2, decedent char, % and std difference
mat t2_2m=J(18,3,.)

local r = 1
foreach v in `t2vars1'{
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m==`d' [iweight=ip_weight]
		mat t2_2m[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
	mat t2_2m[`r',3]=(t2_2m[`r',1]/100-t2_2m[`r',2]/100)/sqrt((t2_2m[`r',1]/100* ///
		(1-t2_2m[`r',1]/100)+t2_2m[`r',2]/100*(1-t2_2m[`r',2]/100))/2)
	local r = `r'+1
}
mat rownames t2_2m=`t2vars1'
frmttable, statmat(t2_2m) varlabels ///
	sdec(2) store(tab2_2m) 	

//part 3, cont vars r & s, mean and std diff
mat t2_3m=J(2,5,.)

local r = 1
foreach v in `t2vars2'{
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m==`d' [iweight=ip_weight]
		mat t2_3m[`r',`c']=r(mean)
		mat t2_3m[`r',`c'+1]=r(sd)
		local c = `c'+2		
	}
	mat t2_3m[`r',5]=(t2_3m[`r',1]-t2_3m[`r',3])/sqrt((t2_3m[`r',2]^2+t2_3m[`r',4]^2)/2) 
	local r = `r'+1
}

mat rownames t2_3m=`t2vars2'

mat dmat=(0,1,0,1,0)
mat ann=(1,1,0\1,1,0)
mat list dmat
mat list ann

mat list t2_3

frmttable, statmat(t2_3m) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_3m) 

//part 4, spouse char, %
mat t2_4m=J(14,3,.)

local r = 1
foreach v in `t2vars3'{
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m==`d' [iweight=ip_weight]
		mat t2_4m[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
	mat t2_4m[`r',3]=(t2_4m[`r',1]/100-t2_4m[`r',2]/100)/sqrt((t2_4m[`r',1]/100* ///
		(1-t2_4m[`r',1]/100)+t2_4m[`r',2]/100*(1-t2_4m[`r',2]/100))/2)

	local r = `r'+1
}

mat rownames t2_4m=`t2vars3'
frmttable, statmat(t2_4m) varlabels ///
	sdec(2) store(tab2_4m) 	

outreg, replay(tab2_1m) append(tab2_2m) store(tab2_am)
outreg, replay(tab2_am) append(tab2_3m) store(tab2_bm)
outreg, replay(tab2_bm) append(tab2_4m) store(tab2_cm) //complete matched table w/ std diff

//combine unmatched and match tables
outreg using "`logpath'\10_spo_ip_tab1and2", ///
 replay(tab2_c) merge(tab2_cm) ///
 title("Table 2: Decedent and spouse characteristics used in propensity score matching, by intensive procedure use status") ///
 ctitles("","Unmatched sample","","","Matched sample","","" \ ///
    "","Intensive procedure n (%)","No procedure n (%)","Standardized Difference", ///
    "Intensive procedure %","No procedure %","Standardized Difference") ///
 landscape addtable

*******************************************************************
*******************************************************************
//Table 3 - CES-D score pre/post death by intense procedures use
*******************************************************************
**************************************************************
la var s_cesd_tot_ge3_n1n2 "S Depressed CESD 3+, pre-death,%"
la var s_cesd_tot_ge3_p1 "S Depressed CESD 3+, post-death,%"
la var s_cesdbetter "S CES-D Improved n1 to p1,%"
la var s_cesd_ge3better "Clinical improvement n1 to p1,%"
la var s_cesd_tot_n1n2 "S CES-D Score, pre-death, mean(sd)"
la var s_cesd_tot_p1 "S CES-D Score, post-death, mean(sd)"
la var s_cesdchange "Change CES-D Score n1 to p1, mean(sd)"

local ivars s_cesd_tot_ge3_n1n2 s_cesd_tot_ge3_p1 s_cesdbetter s_cesd_ge3better 
local cvars s_cesd_tot_n1n2 s_cesd_tot_p1 s_cesdchange

//part 1 - prior to matching
mat t2_2m=J(4,4,.)

local r = 1
foreach v in `ivars' { //indicator variables
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m ==`d'
		mat t2_2m[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
	mat t2_2m[`r',3]=(t2_2m[`r',1]/100-t2_2m[`r',2]/100)/sqrt((t2_2m[`r',1]/100* ///
		(1-t2_2m[`r',1]/100)+t2_2m[`r',2]/100*(1-t2_2m[`r',2]/100))/2)
		
	logit int_any_of5_6m `v'
	test `v'
	mat t2_2m[`r',4] = r(p)
	
	local r = `r'+1
}
mat rownames t2_2m= `ivars' 
frmttable, statmat(t2_2m) varlabels ///
	sdec(2) store(tab2_2m) 

mat t2_3m=J(3,6,.)
local r = 1
foreach v in `cvars'  { //continuous variables
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m ==`d', detail 
		mat t2_3m[`r',`c']=r(mean)
		mat t2_3m[`r',`c'+1]=r(sd)
		local c = `c'+2	
	}
	mat t2_3m[`r',5]=(t2_3m[`r',1]-t2_3m[`r',3])/sqrt((t2_3m[`r',2]^2+t2_3m[`r',4]^2)/2) 
	
	logit int_any_of5_6m `v'
	test `v'
	mat t2_3m[`r',6] = r(p)
	
	local r = `r'+1
}
mat rownames t2_3m=`cvars' 

mat dmat=(0,1,0,1,0,0)
mat ann=(1,1,0,0\1,1,0,0\1,1,0,0)
mat list dmat
mat list ann

mat list t2_3m

frmttable, statmat(t2_3m) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_3m) 

outreg using `logpath'\10_spo_ip_tab1and2, ///
replay(tab2_2m) append(tab2_3m) addtable ///
title("Table 3a: Spouse CES-D score details, sample before matching") ///
ctitles("" "Intensive procedure" "No procedure" "Std Diff." "p-value")

//Part 2 - after matching
mat t2_2m=J(4,4,.)

local r = 1
foreach v in `ivars' { //indicator variables
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m ==`d' [iweight=ip_weight]
		mat t2_2m[`r',`c']=r(mean)*100
		local c = `c'+1		
	}
	mat t2_2m[`r',3]=(t2_2m[`r',1]/100-t2_2m[`r',2]/100)/sqrt((t2_2m[`r',1]/100* ///
		(1-t2_2m[`r',1]/100)+t2_2m[`r',2]/100*(1-t2_2m[`r',2]/100))/2)
		
	logit int_any_of5_6m `v' [iw=ip_weight]
	test `v'
	mat t2_2m[`r',4] = r(p)
	
	local r = `r'+1
}
mat rownames t2_2m=`ivars'
frmttable, statmat(t2_2m) varlabels ///
	sdec(2) store(tab2_2m) 

mat t2_3m=J(3,6,.)
local r = 1
foreach v in `cvars'  { //continuous variables
	local c = 1
	foreach d in 1 0 {
		sum `v' if int_any_of5_6m ==`d' [iweight=ip_weight]
		mat t2_3m[`r',`c']=r(mean)
		mat t2_3m[`r',`c'+1]=r(sd)
		local c = `c'+2	
	}
	mat t2_3m[`r',5]=(t2_3m[`r',1]-t2_3m[`r',3])/sqrt((t2_3m[`r',2]^2+t2_3m[`r',4]^2)/2) 
	
	logit int_any_of5_6m `v' [iw=ip_weight]
	test `v'
	mat t2_3m[`r',6] = r(p)
	
	local r = `r'+1
}
mat rownames t2_3m=`cvars' 

mat dmat=(0,1,0,1,0,0)
mat ann=(1,1,0,0\1,1,0,0\1,1,0,0)
mat list dmat
mat list ann

mat list t2_3m

frmttable, statmat(t2_3m) varlabels ///
	sdec(2) doubles(dmat) ///
	dbldiv(" (") annotate(ann) asymbol(")") ///
	store(tab2_3m) 

outreg using `logpath'\10_spo_ip_tab1and2, ///
replay(tab2_2m) append(tab2_3m) addtable ///
title("Table 3b: Spouse CES-D score details, matched sample") ///
ctitles("" "Intensive procedure" "No procedure" "Std Diff." "p-value")

//test pre-post cesd score difference, not matched, full sample
ttest s_cesd_tot_p1=s_cesd_tot_n1n2   //overall sample
ttest s_cesd_tot_p1=s_cesd_tot_n1n2 if ///
 (r_adl_sp_helper_x==1 | r_iadl_sp_helper_x==1) //caregiver sample
ttest s_cesdchange if  (r_adl_sp_helper_x==1 | r_iadl_sp_helper_x==1), ///
 by(int_any_of5_6m)  //caregiver sample

***************************************************************
log close


H="notes"
Evan's project used:
Age at n1 core interview
Marital status at n1 core interview

For spouse depression, we used age at death, filtered on marital status at death

If n1 core was within the 6m preceding death, then used n2 core for the following:
Nursing home residence
Relative living nearby
Functional status

For chronic conditions, Evan controlled for conditions 6-12m before death
We currently have them coded for the full last 2 years of life


*******************************************************
Tables (based on spouse hospice depression paper)

Table 1 - Sample characteristics, overall before matching

Table 2 - Variables in propensity score matching only by intensive procedure use
Both before and after matching with percent standardized difference reported

Table 3 - Results
Think about how to split
Full sample / Excluding Hospice
Primary caregiver spouses including / excluding hospice users

H="********************************"
